类 编辑器_行为编辑器_编辑 : 窗口

	@布局配置([[根布局=真,宽度=-1,高度=-1]])
	变量 自适应布局1 : 自适应布局
	@布局配置([[父布局=自适应布局1,宽度=-1,高度=-1,@横坐标比例=-9.0E-4,@纵坐标比例=-4.0E-4,纵向布局=真]])
	变量 线性布局1 : 线性布局
	@布局配置([[父布局=线性布局1,宽度=-1,高度DP=50,背景颜色=-1,纵向布局=假]])
	变量 线性布局2 : 线性布局
	@布局配置([[父布局=线性布局2,宽度DP=50,高度DP=50,对齐方式=17]])
	变量 线性布局3 : 线性布局
	@布局配置([[父布局=线性布局3,宽度DP=25,高度DP=25,图片资源="../../../../资源库/图标/back/default_backicon.png",水波纹效果=真,可获取焦点=真]])
	变量 退出界面 : 图片框
	@布局配置([[父布局=线性布局2,宽度=-1,高度=-1]])
	变量 相对布局1 : 相对布局
	@布局配置([[父布局=相对布局1,高度DP=50,对齐方式=8388627,宽度=-1]])
	变量 线性布局6 : 线性布局
	@布局配置([[父布局=线性布局6,字体大小=20,粗体=假,@左外边距DP=10,字体颜色=-16777216,可视=真,内容="编辑模式",@下外边距DP=5]])
	变量 文本框2 : 文本框
	@布局配置([[父布局=线性布局6,宽度=-1,@左外边距DP=10,@右外边距DP=50,高度=-1,背景颜色=-1,提示文本="搜索项名称",内容="",左内边距DP=5,右内边距DP=5,字体大小=17,可视=假,单行显示=真]])
	变量 编辑框1 : 编辑框
	@布局配置([[父布局=相对布局1,高度DP=50,对齐方式=8388629,宽度=-1]])
	变量 线性布局4 : 线性布局
	@布局配置([[父布局=线性布局4,宽度DP=50,高度DP=50,对齐方式=17,水波纹效果=真,纵向布局=真]])
	变量 线性布局7 : 线性布局
	@布局配置([[父布局=线性布局7,宽度DP=25,高度DP=25,图片资源="../../../../资源库/图标/editor/behavior_editor/editor/save.xml",可视=真]])
	变量 图片框1 : 图片框
	@布局配置([[父布局=线性布局7,宽度DP=25,高度DP=25,可视=假]])
	变量 进度圈1 : 进度圈
	@布局配置([[父布局=相对布局1,宽度=-1,高度=-1,对齐方式=8388691]])
	变量 线性布局8 : 线性布局
	@布局配置([[父布局=线性布局8,@左外边距DP=10,字体颜色=-16777216,字体大小=12,@下外边距DP=3]])
	变量 文本框1 : 文本框
	@布局配置([[父布局=线性布局1,宽度=-1,高度DP=40,背景颜色=-1,显示滚动条=假,完全显示=假]])
	变量 横向滚动布局1 : 横向滚动布局
	@布局配置([[父布局=横向滚动布局1,宽度=-1,高度=-1,对齐方式=8388627]])
	变量 线性布局9 : 线性布局
	@布局配置([[父布局=线性布局9,宽度DP=40,高度DP=40,对齐方式=17,水波纹效果=真]])
	变量 线性布局10 : 线性布局
	@布局配置([[父布局=线性布局10,宽度DP=25,高度DP=25,图片资源="../../../../资源库/图标/editor/behavior_editor/editor/keyboard.xml"]])
	变量 图片框2 : 图片框
	@布局配置([[父布局=线性布局9,宽度DP=40,高度DP=40,对齐方式=17,水波纹效果=真]])
	变量 线性布局11 : 线性布局
	@布局配置([[父布局=线性布局11,宽度DP=25,高度DP=25,图片资源="../../../../资源库/图标/editor/behavior_editor/editor/undo.xml"]])
	变量 图片框3 : 图片框
	@布局配置([[父布局=线性布局9,宽度DP=40,高度DP=40,对齐方式=17,水波纹效果=真]])
	变量 线性布局12 : 线性布局
	@布局配置([[父布局=线性布局12,宽度DP=25,高度DP=25,图片资源="../../../../资源库/图标/editor/behavior_editor/editor/redo.xml"]])
	变量 图片框4 : 图片框
	@布局配置([[父布局=线性布局9,宽度DP=40,高度DP=40,对齐方式=17,水波纹效果=真]])
	变量 线性布局13 : 线性布局
	@布局配置([[父布局=线性布局13,宽度DP=25,高度DP=25,图片资源="../../../../资源库/图标/editor/behavior_editor/editor/copy.xml"]])
	变量 图片框5 : 图片框
	@布局配置([[父布局=线性布局9,宽度DP=40,高度DP=40,对齐方式=17,水波纹效果=真]])
	变量 线性布局14 : 线性布局
	@布局配置([[父布局=线性布局14,宽度DP=25,高度DP=25,图片资源="../../../../资源库/图标/editor/behavior_editor/editor/select_all.xml"]])
	变量 图片框6 : 图片框
	@布局配置([[父布局=线性布局9,宽度DP=40,高度DP=40,对齐方式=17,水波纹效果=真]])
	变量 线性布局15 : 线性布局
	@布局配置([[父布局=线性布局15,宽度DP=25,高度DP=25,图片资源="../../../../资源库/图标/editor/behavior_editor/editor/format.xml"]])
	变量 图片框7 : 图片框
	@布局配置([[父布局=线性布局9,宽度DP=40,高度DP=40,对齐方式=17,水波纹效果=真]])
	变量 线性布局16 : 线性布局
	@布局配置([[父布局=线性布局16,宽度DP=25,高度DP=25,图片资源="../../../../资源库/图标/editor/behavior_editor/editor/palette.xml"]])
	变量 图片框8 : 图片框
	@布局配置([[父布局=线性布局9,宽度DP=40,高度DP=40,对齐方式=17,水波纹效果=真]])
	变量 线性布局17 : 线性布局
	@布局配置([[父布局=线性布局17,宽度DP=25,高度DP=25,图片资源="../../../../资源库/图标/editor/behavior_editor/editor/search.xml"]])
	变量 图片框9 : 图片框
	@布局配置([[父布局=线性布局9,宽度DP=40,高度DP=40,对齐方式=17,水波纹效果=真]])
	变量 线性布局18 : 线性布局
	@布局配置([[父布局=线性布局18,宽度DP=25,高度DP=25,图片资源="../../../../资源库/图标/editor/behavior_editor/editor/substitution.xml"]])
	变量 图片框10 : 图片框
	@布局配置([[父布局=线性布局9,宽度DP=40,高度DP=40,对齐方式=17,水波纹效果=真]])
	变量 线性布局19 : 线性布局
	@布局配置([[父布局=线性布局19,宽度DP=25,高度DP=25,图片资源="../../../../资源库/图标/editor/behavior_editor/editor/inspect.xml"]])
	变量 图片框11 : 图片框
	@布局配置([[父布局=线性布局9,宽度DP=40,高度DP=40,对齐方式=17,水波纹效果=真]])
	变量 线性布局20 : 线性布局
	@布局配置([[父布局=线性布局20,宽度DP=25,高度DP=25,图片资源="../../../../资源库/图标/editor/behavior_editor/editor/translate.xml"]])
	变量 图片框12 : 图片框
	@布局配置([[父布局=线性布局1,宽度=-1,高度DP=40,背景颜色=-1,显示滚动条=假,完全显示=假,可视=假]])
	变量 横向滚动布局2 : 横向滚动布局
	@布局配置([[父布局=横向滚动布局2,宽度=-1,高度=-1,对齐方式=8388627]])
	变量 线性布局21 : 线性布局
	@布局配置([[父布局=线性布局1,宽度=-1,高度=-2,背景颜色=-1,纵向布局=真]])
	变量 线性布局22 : 线性布局
	@布局配置([[父布局=线性布局22,宽度=-1,高度DP=40,可视=假]])
	变量 线性布局23 : 线性布局
	@布局配置([[父布局=线性布局23,高度DP=40,宽度DP=40,对齐方式=17,水波纹效果=真]])
	变量 线性布局24 : 线性布局
	@布局配置([[父布局=线性布局24,内容="查找",粗体=真,字体颜色=-16777216]])
	变量 文本框3 : 文本框
	@布局配置([[父布局=线性布局23,宽度=-1,高度=-1,对齐方式=8388627]])
	变量 线性布局25 : 线性布局
	@布局配置([[父布局=线性布局25,宽度=-1,高度=-1,背景颜色=0,左内边距DP=5,右内边距DP=5,内容="",提示文本="查找内容...",@左外边距DP=0,@右外边距DP=10,字体大小=15]])
	变量 编辑框2 : 编辑框
	@布局配置([[父布局=线性布局22,宽度=-1,高度DP=40,可视=假]])
	变量 线性布局26 : 线性布局
	@布局配置([[父布局=线性布局26,高度DP=40,宽度DP=40,对齐方式=17,水波纹效果=真]])
	变量 线性布局27 : 线性布局
	@布局配置([[父布局=线性布局27,内容="替换",粗体=真,字体颜色=-16777216]])
	变量 文本框4 : 文本框
	@布局配置([[父布局=线性布局26,宽度=-1,高度=-1,对齐方式=8388627]])
	变量 线性布局28 : 线性布局
	@布局配置([[父布局=线性布局28,宽度=-1,高度=-1,背景颜色=0,左内边距DP=5,右内边距DP=5,内容="",提示文本="新的内容...",@左外边距DP=0,@右外边距DP=10,字体大小=15]])
	变量 编辑框3 : 编辑框
	@布局配置([[父布局=线性布局1,宽度=-1,高度=-1,背景颜色=-1]])
	变量 线性布局5 : 线性布局
	@布局配置([[父布局=线性布局5,宽度=-1,高度=-1]])
	变量 代码框1 : 代码框

	//声明
	变量 设置_设定  : 设置_设定

	变量 文件路径 : 文本

	事件 编辑器_行为编辑器_编辑:创建完毕()
		//设置主题
		本对象.显示标题栏 = 假
		本对象.状态栏颜色 = 0XFFFFFFFF
		本对象.状态栏字体黑色 = 真
		//窗口切换动画
		播放默认切换动画(2)
		//设置夜间模式
		如果 设置_设定.获取开关("NightMode") 则
			设置夜间模式()
		结束 如果
		//退出按钮事件
		事件 退出界面:被单击()
			关闭窗口()
		结束 事件
		//加载方法
		菜单()
		//加载文件
		文件路径 = 其他_其他.读缓存("behaviour_editor_path_code")
		文本框1.内容 = 取文件名(文件路径)
		变量 文件内容 : 文本 = 读入文本文件(文件路径)
		代码框1.设置内容(文件内容)
		//自动保存
		如果 设置_设定.获取开关("BehaviourEditorSelectEditAutoSave") 则
			自动保存()
		结束 如果
		//设置代码框主题
		如果 设置_设定.获取开关("NightMode") 则
			代码框1.设置字符串颜色(0XFFBD63C5)
			代码框1.设置用户字(0XFFFFFFFF)
			代码框1.设置操作符颜色(0XFFFFFFFF)
			代码框1.设置注释颜色(0XFF3F7F5F)
			代码框1.设置行号颜色(0XFFBEC0C1)
			代码框1.设置选择文本的背景色(0XB81795D4)
		否则
			代码框1.背景颜色 = 0XFFFFFFFF
			代码框1.设置字符串颜色(0XFF336699)
			代码框1.设置用户字(0XFF000000)
			代码框1.设置操作符颜色(0XFF000000)
			代码框1.设置注释颜色(0XFF3F7F5F)
			代码框1.设置行号颜色(0XFFBEC0C1)
			代码框1.设置选择文本的背景色(0XFFDDE0F2)
		结束 如果
		//设置背景
		变量 设置相关路径 : 文本 = 设置_设定.获取文本("PersonalityBackground")
		如果 设置相关路径.替换(" ","") == "" 则
		否则
			如果 文件是否存在(设置相关路径) 则
				设置_设置.设置布局背景(本对象,代码框1,设置相关路径)
			结束 如果
		结束 如果
		//订阅事件
		订阅事件()
	结束 事件

	//上方功能区菜单
	变量 符号栏a : 文本
	变量 符号栏开关 : 逻辑型 = 假
	变量 查找开关 : 逻辑型 = 假
	变量 替换开关 : 逻辑型 = 假
	方法 菜单()
		//保存
		事件 线性布局7:被单击()
			变量 项目名称 : 文本 = 其他_其他.读缓存("ProjectName")
			变量 输出路径 : 文本 = 其他_变量.项目目录 + 项目名称 + "/behaviour/" + 其他_其他.读缓存("behaviour_editor_outputpath")
			如果 文件是否存在(输出路径) 则
				变量 覆盖提示 : DX通知
				//设置夜间模式
				如果 设置_设定.获取开关("NightMode") 则
					覆盖提示.主题(DX主题.暗色)
				结束 如果
				覆盖提示.警告图标()
				覆盖提示.标题("提示")
				覆盖提示.内容("有相同的文件，是否覆盖它")
				覆盖提示.按钮("覆盖")
				覆盖提示.显示()
				事件 覆盖提示:按钮被单击() : 逻辑型
					写出文本文件(输出路径,代码框1.获取内容())
					项目记录生成(输出路径)
					编辑器_行为编辑器_功能.生成操作记录(其他_其他.读缓存("behaviour_editor_outputpath"))
					弹出成功通知("文件覆盖完毕！")
					返回 假
				结束 事件
			否则
				写出文本文件(输出路径,代码框1.获取内容())
				项目记录生成(输出路径)
				编辑器_行为编辑器_功能.生成操作记录(其他_其他.读缓存("behaviour_editor_outputpath"))
				弹出成功通知("项目保存完毕！")
			结束 如果
		结束 事件
		//符号
		事件 线性布局10:被单击()
			如果 符号栏开关 则
				符号栏开关 = 假
				横向滚动布局2.可视 = 假
			否则
				符号栏开关 = 真
				横向滚动布局2.可视 = 真
				//加载
				变量 符号栏内容 : 文本 = 设置_设定.获取文本("BehaviourEditorSelectEditSymbolsField")
				变量 符号栏sym : 文本[] = 符号栏内容.分割文本(" ")
				线性布局21.移除所有组件()
				循环(i,0,取数组长度(符号栏sym))
					变量 符号栏an : 按钮
					符号栏an.单行显示 = 真
					符号栏a = 符号栏sym[i]
					线性布局21.添加组件(符号栏an)
					如果 设置_设定.获取开关("NightMode") 则
						符号栏an.背景颜色 = 0XFF222222
						符号栏an.字体颜色 = 0XFFFFFFFF
					否则
						符号栏an.背景颜色 = 白色
					结束 如果
					符号栏an.高度 = -1
					符号栏an.内容 = 符号栏a
					符号栏an.内边距 = 2
					符号栏an.宽度DP = 40
					如果 设置_设定.获取开关("NightMode") 则
						组件样式操作.置水波纹样式(符号栏an,0,0XFF222222,灰色)
					否则
						组件样式操作.置水波纹样式(符号栏an,0,白色,灰色)
					结束 如果
					//符号栏插入文本
					事件 符号栏an:被单击()
						变量 符号栏anns : 文本 = 符号栏an.内容
						代码框1.插入文本(代码框1.获取光标位置(),符号栏anns)
					结束 事件
				结束 循环
			结束 如果
		结束 事件
		//撤销
		事件 线性布局11:被单击()
			代码框1.撤销()
		结束 事件
		//重做
		事件 线性布局12:被单击()
			代码框1.重做()
		结束 事件
		//选中复制
		事件 线性布局13:被单击()
			变量 选中复制内容 : 文本 = 代码框1.取选中文本()
			如果 选中复制内容 == 空 则
				弹出警告提示("提示：你未选中文字")
			否则
				如果 选中复制内容 == "" 则
					弹出警告提示("提示：你未选中文字")
				否则
					置剪切板文本(本对象,选中复制内容)
					弹出成功提示("复制完毕！")
				结束 如果
			结束 如果
		结束 事件
		//全选
		事件 线性布局14:被单击()
			代码框1.全选()
		结束 事件
		//格式化
		事件 线性布局15:被单击()
			开始俘获异常()
			变量 json : JSON对象 = 代码框1.获取内容()
			代码框1.设置内容(json.到文本(设置_设定.获取整数("BehaviourEditorSelectEditIndent")))
			俘获所有异常()
			弹出错误通知("格式化失败，请检查你的代码是否有问题！")
			结束俘获异常()
		结束 事件
		//颜色
		事件 线性布局16:被单击()
			变量 颜色对话框 : DX基础对话框
			变量 颜色UI : 编辑器_行为编辑器_编辑_颜色
			颜色对话框.标题("颜色")
			//设置夜间模式
			如果 设置_设定.获取开关("NightMode") 则
				颜色对话框.主题(DX主题.暗色)
				颜色UI.文本框1.字体颜色 = 0XFFFFFFFF
			结束 如果
			颜色对话框.自定义布局(颜色UI)
			颜色对话框.按钮1("确定")
			颜色对话框.按钮2("关闭")
			//设置布局
			颜色UI.调色盘1.设置初始颜色 = -1
			//选择
			事件 颜色UI.调色盘1:调色盘被触摸(颜色 : 整数,用户触摸 : 逻辑型,来自应用 : 逻辑型)
				变量 颜色值 : 文本 = 颜色操作.颜色值到文本(颜色,真,真)
				颜色UI.文本框1.字体颜色 = 颜色
				颜色UI.文本框1.内容 = 颜色值
				事件 颜色对话框:按钮1被单击() : 逻辑型
					代码框1.插入文本(代码框1.获取光标位置(),颜色值)
					返回 假
				结束 事件
			结束 事件
			//显示
			颜色对话框.显示()
		结束 事件
		//查找
		事件 线性布局17:被单击()
			如果 查找开关 则
				查找开关 = 假
				线性布局23.可视 = 假
			否则
				查找开关 = 真
				线性布局23.可视 = 真
				事件 线性布局24:被单击()
					代码框1.查找下一个(编辑框2.内容)
				结束 事件
			结束 如果
		结束 事件
		//替换
		事件 线性布局18:被单击()
			如果 替换开关 则
				替换开关 = 假
				线性布局26.可视 = 假
			否则
				替换开关 = 真
				线性布局23.可视 = 真
				线性布局26.可视 = 真
				事件 线性布局27:被单击()
					变量 e1 : 文本 = 编辑框2.内容
					变量 e2 : 文本 = 编辑框3.内容
					代码框1.设置内容(代码框1.获取内容().替换(e1,e2))
				结束 事件
			结束 如果
		结束 事件
		//检查
		事件 线性布局19:被单击()
			开始俘获异常()
			变量 json : JSON对象 = 代码框1.获取内容()
			调试输出(json.到文本(4))
			弹出成功提示("你的代码没有任何错误！")
			俘获所有异常()
			变量 查看菜单 : DX底部对话框
			如果 设置_设定.获取开关("NightMode") 则
				查看菜单.主题(DX主题.暗色)
			结束 如果
			查看菜单.标题("错误日志")
			查看菜单.内容("异常信息：\n" + 取俘获异常().取异常信息())
			查看菜单.按钮1("关闭")
			查看菜单.显示()
			事件 查看菜单:按钮1被单击() : 逻辑型
				查看菜单.关闭()
				返回 假
			结束 事件
			结束俘获异常()
		结束 事件
		//翻译
		事件 线性布局20:被单击()
			变量 网络翻译 : 网络工具
			网络翻译.发送数据("http://m.youdao.com/translate","inputtext=" + 代码框1.取选中文本().替换("_"," ") + "&type=" + "EN2ZH_CN")
			事件 网络翻译:发送数据结束(结果 : 文本,内容 : 字节[],cookie : 文本)
				变量 结果2 : 文本 = 结果.截取文本("译文","</ul>")
				变量 结果3 : 文本 = 结果2.截取文本("<li>","</li>")
				弹出成功通知("翻译成功：" + 结果3)
			结束 事件
			事件 网络翻译:取网页源码失败()
				弹出错误提示("翻译失败！")
			结束 事件
		结束 事件
	结束 方法

	变量 自动保存次数 : 整数 = 0
	方法 自动保存()
		变量 计时器 : 倒计时器
		计时器.开始倒计时(5000l,1000l)
		事件 计时器:倒计时结束()
			变量 项目名称 : 文本 = 其他_其他.读缓存("ProjectName")
			变量 输出路径 : 文本 = 其他_变量.项目目录 + 项目名称 + "/behaviour/" + 其他_其他.读缓存("behaviour_editor_outputpath")
			写出文本文件(输出路径,代码框1.获取内容())
			文本框1.内容 = ""
			自动保存次数 = 自动保存次数 + 1
			文本框1.内容 = 文本框1.内容 + " 自动保存次数：" + 自动保存次数
			计时器.开始倒计时(5000l,1000l)
		结束 事件
	结束 方法

	方法 项目记录生成(路径 : 文本)
		变量 模块_项目 : 模块_项目
		模块_项目.初始化()
		模块_项目.assets图标 = "icon/item/json.png"
		模块_项目.名称 = 取文件前缀名(路径)
		模块_项目.标识符 = 取文件前缀名(路径)
		模块_项目.描述 = "这个是行为包JSON"
		模块_项目.种类 = 模块_项目种类.行为包
		模块_项目.分类 = "json"
		模块_项目.路径 = 路径
		模块_项目.保存()
	结束 方法

	//设置功能按钮颜色
	方法 设置夜间模式()
		本对象.状态栏颜色 = 0XFF333333
		本对象.状态栏字体黑色 = 假
		线性布局2.背景颜色 = 0XFF333333
		编辑框1.背景颜色 = 0XFF333333
		退出界面.图片资源 = 编辑器_行为编辑器_资源.白色返回图标
		图片框1.图片资源 = 编辑器_行为编辑器_编辑_资源.白色保存图标
		文本框1.字体颜色 = 0XFFFFFFFF
		文本框2.字体颜色 = 0XFFFFFFFF
		文本框3.字体颜色 = 0XFFFFFFFF
		文本框4.字体颜色 = 0XFFFFFFFF
		线性布局5.背景颜色 = 0XFF222222
		编辑框1.提示文本颜色 = 0XFF999999
		编辑框1.字体颜色 = 0XFFFFFFFF
		横向滚动布局1.背景颜色 = 0XFF222222
		横向滚动布局2.背景颜色 = 0XFF222222 
		线性布局22.背景颜色 = 0XFF222222
		编辑框2.字体颜色 = 0XFFFFFFFF
		编辑框2.提示文本颜色 = 0XFFA2A2A2
		编辑框3.字体颜色 = 0XFFFFFFFF 
		编辑框3.提示文本颜色 = 0XFFA2A2A2
		代码框1.背景颜色 = 0XFF222222
		图片框2.图片资源 = 编辑器_行为编辑器_编辑_资源.白色符号图标
		图片框3.图片资源 = 编辑器_行为编辑器_编辑_资源.白色撤销图标
		图片框4.图片资源 = 编辑器_行为编辑器_编辑_资源.白色重做图标
		图片框5.图片资源 = 编辑器_行为编辑器_编辑_资源.白色复制图标
		图片框6.图片资源 = 编辑器_行为编辑器_编辑_资源.白色全选图标
		图片框7.图片资源 = 编辑器_行为编辑器_编辑_资源.白色格式化图标
		图片框8.图片资源 = 编辑器_行为编辑器_编辑_资源.白色颜色图标
		图片框9.图片资源 = 编辑器_行为编辑器_编辑_资源.白色查找图标
		图片框10.图片资源 = 编辑器_行为编辑器_编辑_资源.白色替换图标
		图片框11.图片资源 = 编辑器_行为编辑器_编辑_资源.白色检查图标
		图片框12.图片资源 = 编辑器_行为编辑器_编辑_资源.白色翻译图标
	结束 方法

结束 类

类 编辑器_行为编辑器_编辑_颜色 : 组件容器

	@布局配置([[根布局=真,宽度=-1,高度=-2,纵向布局=真]])
	变量 线性布局1 : 线性布局
	@布局配置([[父布局=线性布局1,宽度=-1,高度=-2,@上外边距DP=5]])
	变量 线性布局2 : 线性布局
	@布局配置([[父布局=线性布局2,宽度=-1,高度=900,@布局对齐方式=17]])
	变量 相对布局1 : 相对布局
	@布局配置([[父布局=相对布局1,宽度=-1,高度=-1,设置初始颜色=-1,显示明暗拖动条=真,显示透明度拖动条=真,@左外边距DP=20,@右外边距DP=20,@位于布局中间=真]])
	变量 调色盘1 : 调色盘
	@布局配置([[父布局=线性布局1,宽度=-1,高度DP=20,对齐方式=8388627,@上外边距DP=3]])
	变量 线性布局3 : 线性布局
	@布局配置([[父布局=线性布局3,@左外边距DP=20,@右外边距DP=20,粗体=假,字体大小=14,字体颜色=-16777216]])
	变量 文本框1 : 文本框

结束 类 

类 编辑器_行为编辑器_编辑_资源

	@静态
	变量 白色保存图标 : 图片资源 =  "../../../../资源库/图标/editor/behavior_editor/editor/save2.xml"

	@静态
	变量 白色符号图标 : 图片资源 =  "../../../../资源库/图标/editor/behavior_editor/editor/keyboard2.xml"

	@静态
	变量 白色撤销图标 : 图片资源 =  "../../../../资源库/图标/editor/behavior_editor/editor/undo2.xml"

	@静态
	变量 白色重做图标 : 图片资源 =  "../../../../资源库/图标/editor/behavior_editor/editor/redo2.xml"

	@静态
	变量 白色复制图标 : 图片资源 =  "../../../../资源库/图标/editor/behavior_editor/editor/copy2.xml"

	@静态
	变量 白色全选图标 : 图片资源 =  "../../../../资源库/图标/editor/behavior_editor/editor/select_all2.xml"

	@静态
	变量 白色格式化图标 : 图片资源 =  "../../../../资源库/图标/editor/behavior_editor/editor/format2.xml"

	@静态
	变量 白色颜色图标 : 图片资源 =  "../../../../资源库/图标/editor/behavior_editor/editor/palette2.xml"

	@静态
	变量 白色查找图标 : 图片资源 =  "../../../../资源库/图标/editor/behavior_editor/editor/search2.xml"

	@静态
	变量 白色替换图标 : 图片资源 =  "../../../../资源库/图标/editor/behavior_editor/editor/substitution2.xml"

	@静态
	变量 白色检查图标 : 图片资源 =  "../../../../资源库/图标/editor/behavior_editor/editor/inspect2.xml"

	@静态
	变量 白色翻译图标 : 图片资源 =  "../../../../资源库/图标/editor/behavior_editor/editor/translate2.xml"

结束 类