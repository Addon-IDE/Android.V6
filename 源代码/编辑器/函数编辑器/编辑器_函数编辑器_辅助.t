类 编辑器_函数编辑器_辅助
	
	//声明
	变量 设置_设定 : 设置_设定
	
	变量 安卓环境名 : 安卓环境?
	变量 位置名 : 线性布局?
	变量 代码框名称 : CodeEditorView?
	
	//设置
	方法 设置环境名(环境名 : 安卓环境)
		安卓环境名 = 环境名
	结束 方法
	方法 设置位置(位置 : 线性布局)
		位置名 = 位置
	结束 方法
	方法 设置代码框(代码框 : CodeEditorView)
		代码框名称 = 代码框
	结束 方法
	
	//菜单
	方法 菜单()
		变量 主菜单 : DX菜单
		变量 菜单布局 : 编辑器_函数编辑器_辅助布局 = 新建窗口组件(编辑器_函数编辑器_辅助布局,安卓环境名)
		如果 设置_设定.获取开关("NightMode") 则
			主菜单.主题(DX主题.暗色)
		结束 如果
		主菜单.宽度(400)
		主菜单.高度(400)
		主菜单.依附组件(位置名)
		主菜单.入场动画时长(设置_设定.获取整数("CodeEditorMenuAnimationDuration"))
		主菜单.依附位置(DX对齐方式.下)
		主菜单.自定义布局(菜单布局)
		主菜单.显示()
		//设置夜间模式
		如果 设置_设定.获取开关("NightMode") 则
			菜单布局.滚动布局1.背景颜色 = 0XFF333333
			菜单布局.文本框1.字体颜色 = 0XFFFFFFFF
			菜单布局.文本框2.字体颜色 = 0XFFFFFFFF
			菜单布局.文本框3.字体颜色 = 0XFFFFFFFF
			菜单布局.文本框4.字体颜色 = 0XFFFFFFFF
		结束 如果
		//跳转到行
		事件 菜单布局.线性布局3:被单击()
			主菜单.关闭()
			跳转到行()
		结束 事件
		//ID查询
		事件 菜单布局.线性布局4:被单击()
			主菜单.关闭()
			打开浏览器("https://ca.projectxero.top/idlist/")
		结束 事件
		//指令查询
		事件 菜单布局.线性布局5:被单击()
			主菜单.关闭()
			变量 当前行的位置 : 整数 = 代码框名称.获取当前行的位置
			变量 链接 : 文本 = "https://minecraft.fandom.com/zh/wiki/%E5%91%BD%E4%BB%A4/" + 代码框名称.获取当前行的文本(当前行的位置)
			打开浏览器(链接)
		结束 事件
	结束 方法
	
	方法 跳转到行()
		//对话框
		变量 操作菜单 : DX输入对话框
		变量 获取当前行的位置 : 整数 = 代码框名称.获取当前行的位置+1
		操作菜单.样式(DX样式.MIUI)
		如果 设置_设定.获取开关("NightMode") 则
			操作菜单.主题(DX主题.暗色)
		结束 如果
		操作菜单.标题("跳转到行")
		操作菜单.内容("当前：" + 获取当前行的位置 + " 总共：" + 代码框名称.获取行数)
		操作菜单.输入内容("0")
		操作菜单.自动弹出键盘(假)
		操作菜单.按钮1("跳转")
		操作菜单.按钮2("取消")
		操作菜单.显示()
		//跳转
		事件 操作菜单:按钮1被单击(文本 : 文本) : 逻辑型
			开始俘获异常()
			变量 编辑框内容 : 整数 = 操作菜单.取输入内容.到整数()
			如果 编辑框内容.到文本().替换(" ","") == "" 则
			否则
				如果 编辑框内容 <= 0 则
					代码框名称.跳转行(编辑框内容)
				否则
					如果 编辑框内容 <= 代码框名称.获取行数-1 则
						代码框名称.跳转行(编辑框内容 -1)
					否则
						代码框名称.跳转行(代码框名称.获取行数-1)
					结束 如果
				结束 如果
			结束 如果
			俘获所有异常()
			结束俘获异常()
			返回 假
		结束 事件
	结束 方法
	
	方法 打开浏览器(地址 : 文本)
		//对话框
		变量 主菜单 : DX底部对话框
		变量 菜单布局 : 编辑器_函数编辑器_浏览器布局 = 新建窗口组件(编辑器_函数编辑器_浏览器布局,安卓环境名)
		如果 设置_设定.获取开关("NightMode") 则
			主菜单.主题(DX主题.暗色)
		结束 如果
		主菜单.标题(菜单布局.浏览框1.标题)
		主菜单.自定义布局(菜单布局)
		主菜单.按钮1("关闭")
		主菜单.显示()
		//浏览器
		菜单布局.浏览框1.高度DP = 设置_设定.获取整数("FunctionEditorBrowserHeight")
		菜单布局.浏览框1.网址 = 地址
	结束 方法
	
结束 类

类 编辑器_函数编辑器_辅助布局 : 组件容器

	@布局配置([[根布局=真,宽度=-1,高度=-2]])
	变量 线性布局1 : 线性布局
	@布局配置([[父布局=线性布局1,宽度=-1,高度=-1,背景颜色=-1]])
	变量 滚动布局1 : 滚动布局
	@布局配置([[父布局=滚动布局1,宽度=-1,高度=-1,纵向布局=真]])
	变量 线性布局2 : 线性布局
	@布局配置([[父布局=线性布局2,宽度=-1,高度DP=45,对齐方式=8388627,水波纹效果=真]])
	变量 线性布局3 : 线性布局
	@布局配置([[父布局=线性布局3,@左外边距DP=10,字体大小=15,字体颜色=-16777216,内容="跳转到行"]])
	变量 文本框1 : 文本框
	@布局配置([[父布局=线性布局2,宽度=-1,高度DP=45,对齐方式=8388627,水波纹效果=真]])
	变量 线性布局4 : 线性布局
	@布局配置([[父布局=线性布局4,@左外边距DP=10,字体大小=15,字体颜色=-16777216,内容="ID查询"]])
	变量 文本框2 : 文本框
	@布局配置([[父布局=线性布局2,宽度=-1,高度DP=45,对齐方式=8388627,水波纹效果=真]])
	变量 线性布局5 : 线性布局
	@布局配置([[父布局=线性布局5,@左外边距DP=10,字体大小=15,字体颜色=-16777216,内容="指令查询"]])
	变量 文本框3 : 文本框
	@布局配置([[父布局=线性布局2,宽度=-1,高度DP=45,对齐方式=8388627,水波纹效果=真]])
	变量 线性布局6 : 线性布局
	@布局配置([[父布局=线性布局6,@左外边距DP=10,字体大小=15,字体颜色=-16777216,内容="添加文件备注(目前不可用)"]])
	变量 文本框4 : 文本框

结束 类

类 编辑器_函数编辑器_浏览器布局 : 组件容器

	@布局配置([[根布局=真,宽度=-1,高度=-1]])
	变量 线性布局1 : 线性布局
	@布局配置([[父布局=线性布局1,宽度=-1,高度DP=500]])
	变量 浏览框1 : 浏览框

	
结束 类