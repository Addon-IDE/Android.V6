类 编辑器_代码编辑器_文件菜单

	//声明
	变量 设置_设定 : 设置_设定

	//变量
	变量 列表菜单项目 : 文本[]

	方法 菜单(布局名称 : 线性布局,文件路径 : 文本,安卓环境名 : 安卓环境)
		//菜单
		变量 XPopup : XPopup附着弹窗 = 新建窗口组件(XPopup附着弹窗,安卓环境名)
		变量 菜单项目 : 文本[] = {"刷新列表","新建文件夹","新建文件"}
		XPopup.附着组件 = 布局名称
		XPopup.弹出位置(XPopup位置.下)
		XPopup.动画(XPopup动画.滑动_右上)
		XPopup.半透明(假)
		XPopup.亮色状态栏(真)
		//设置夜间模式
		如果 设置_设定.获取开关("NightMode") 则
			XPopup.暗色主题(真)
		结束 如果
		XPopup.列表项目 = 菜单项目
		XPopup.显示()
		//列表被点击
		事件 XPopup:列表被单击(索引 : 整数,文本 : 文本)
			假如 索引
				是 0
					刷新或成功事件("刷新列表成功",1)
				是 1
					新建(安卓环境名,文件路径,1)
				是 2
					新建(安卓环境名,文件路径,0)
			结束 假如
		结束 事件
	结束 方法

	方法 列表菜单(布局名称 : 线性布局,文件路径 : 文本,安卓环境名 : 安卓环境)
		//菜单
		变量 XPopup : XPopup附着弹窗 = 新建窗口组件(XPopup附着弹窗,安卓环境名)
		XPopup.附着组件 = 布局名称
		XPopup.弹出位置(XPopup位置.下)
		XPopup.宽度(360)
		XPopup.水平居中(真)
		XPopup.动画(XPopup动画.滑动_左上)
		XPopup.半透明(假)
		XPopup.亮色状态栏(真)
		//设置夜间模式
		如果 设置_设定.获取开关("NightMode") 则
			XPopup.暗色主题(真)
		结束 如果
		变量 列表菜单项目 : 文本[] = {"重命名","删除"}
		XPopup.列表项目 = 列表菜单项目
		XPopup.显示()
		//列表被点击
		事件 XPopup:列表被单击(索引 : 整数,文本 : 文本)
			假如 索引
				是 0
					重命名(安卓环境名,文件路径)
				是 1
					变量 删除对话框 : XPopup对话弹窗 = 新建窗口组件(XPopup对话弹窗,安卓环境名)
					删除对话框.标题 = "提示"
					删除对话框.内容 = "是否删除这个文件(夹)：\n" + 取文件名(文件路径)
					如果 设置_设定.获取开关("NightMode") 则
						删除对话框.暗色主题(真)
					结束 如果
					删除对话框.显示()
					事件 删除对话框:确定()
						如果 删除文件(文件路径) 则
							刷新或成功事件("文件(夹)删除成功！",1)
						否则
							刷新或成功事件("删除失败！",-1)
						结束 如果
					结束 事件
			结束 假如
		结束 事件
	结束 方法

	//新建
	方法 新建(安卓环境名 : 安卓环境,路径 : 文本,类型 : 整数)
		//菜单
		变量 XPopup : XPopup自定义弹窗 = 新建窗口组件(XPopup自定义弹窗,安卓环境名)
		变量 创建布局 : 编辑器_代码编辑器_布局_新建文件 = 新建窗口组件(编辑器_代码编辑器_布局_新建文件,安卓环境名)
		XPopup.布局(创建布局)
		XPopup.动画(XPopup动画.平移_上进入)
		XPopup.半透明(假)
		XPopup.亮色状态栏(真)
		XPopup.显示()
		//置圆角背景边框
		如果 设置_设定.获取开关("NightMode") 则
			组件样式操作.置圆背景2(创建布局.线性布局1,0XFF333333,25)
			组件样式操作.置圆角背景边框(创建布局.编辑框1,0XFF333333,2,0XFF222222,5,5,5,5)
		否则
			组件样式操作.置圆背景2(创建布局.线性布局1,0XFFFFFFFF,25)
			组件样式操作.置圆角背景边框(创建布局.编辑框1,0xF0FFFFFF,2,0xFFE8E8E8,5,5,5,5)
		结束 如果
		//修改布局
		创建布局.编辑框1.可获取焦点 = 假
		如果 类型 == 0 则
			创建布局.文本框1.内容 = "创建文件"
			创建布局.文本框2.内容 = "请输入文件名"
		否则 类型 == 1
			创建布局.文本框1.内容 = "创建文件夹"
			创建布局.文本框2.内容 = "请输入文件夹名"
		否则
			创建布局.文本框1.内容 = "创建 ?"
			创建布局.文本框2.内容 = "请输入 ? 名"	
		结束 如果
		//设置夜间模式
		如果 设置_设定.获取开关("NightMode") 则
			创建布局.线性布局1.背景颜色 = 0XFF333333
			创建布局.文本框1.字体颜色 = 0XFFFFFFFF
			创建布局.文本框2.字体颜色 = 0XFFE0E0E0
			创建布局.卡片布局2.卡片背景颜色 = 0XFF222222 
			创建布局.线性布局7.背景颜色 = 0XFF222222
			创建布局.文本框4.字体颜色 = 0XFFE0E0E0
			创建布局.编辑框1.字体颜色 = 0XFFFFFFFF
			创建布局.编辑框1.提示文本颜色 = 0XFF999999
		结束 如果
		//确定
		事件 创建布局.线性布局5:被单击()
			变量 最终路径 : 文本 = 路径 + "/" + 创建布局.编辑框1.内容
			如果 文件是否存在(最终路径) 则
			否则
				如果 类型 == 0 则
					写出文本文件(最终路径,"")
					如果 文件是否存在(最终路径) 则
						刷新或成功事件("文件创建成功！",1)
					否则
						刷新或成功事件("文件创建失败！",-1)
					结束 如果
				否则
					写出文本文件(最终路径 + "/null","")
					删除文件(最终路径 + "/null")
					如果 文件是否存在(最终路径) 则
						刷新或成功事件("文件夹创建成功！",1)
					否则
						刷新或成功事件("文件夹创建失败！",-1)
					结束 如果
				结束 如果
				XPopup.关闭()
			结束 如果
		结束 事件
		//关闭
		事件 创建布局.线性布局7:被单击()
			XPopup.关闭()
		结束 事件
	结束 方法

	//重命名
	方法 重命名(安卓环境名 : 安卓环境,路径 : 文本)
		//菜单
		变量 XPopup : XPopup自定义弹窗 = 新建窗口组件(XPopup自定义弹窗,安卓环境名)
		变量 重命名布局 : 编辑器_代码编辑器_布局_重命名文件 = 新建窗口组件(编辑器_代码编辑器_布局_重命名文件,安卓环境名)
		XPopup.布局(重命名布局)
		XPopup.动画(XPopup动画.平移_上进入)
		XPopup.半透明(假)
		XPopup.亮色状态栏(真)
		XPopup.显示()
		//置圆角背景边框
		如果 设置_设定.获取开关("NightMode") 则
			组件样式操作.置圆背景2(重命名布局.线性布局1,0XFF333333,25)
			组件样式操作.置圆角背景边框(重命名布局.编辑框1,0XFF333333,2,0XFF222222,5,5,5,5)
		否则
			组件样式操作.置圆背景2(重命名布局.线性布局1,0XFFFFFFFF,25)
			组件样式操作.置圆角背景边框(重命名布局.编辑框1,0xF0FFFFFF,2,0xFFE8E8E8,5,5,5,5)
		结束 如果
		//修改布局
		重命名布局.编辑框1.可获取焦点 = 假
		重命名布局.编辑框1.内容 = 取文件名(路径)
		重命名布局.编辑框1.光标位置 = 重命名布局.编辑框1.内容.长度
		//设置夜间模式
		如果 设置_设定.获取开关("NightMode") 则
			重命名布局.线性布局1.背景颜色 = 0XFF333333
			重命名布局.文本框1.字体颜色 = 0XFFFFFFFF
			重命名布局.文本框2.字体颜色 = 0XFFE0E0E0
			重命名布局.卡片布局2.卡片背景颜色 = 0XFF222222 
			重命名布局.线性布局7.背景颜色 = 0XFF222222
			重命名布局.文本框4.字体颜色 = 0XFFE0E0E0
			重命名布局.编辑框1.字体颜色 = 0XFFFFFFFF
			重命名布局.编辑框1.提示文本颜色 = 0XFF999999
		结束 如果
		//确定
		事件 重命名布局.线性布局5:被单击()
			如果 重命名布局.编辑框1.内容.替换(" ","") == "" 则
				XPopup.关闭()
			否则
				变量 wj : 文件 = 路径
				变量 flj : 文本 = wj.取父目录路径()
				变量 新路径 : 文本 = flj + "/" + 重命名布局.编辑框1.内容
				如果 重命名文件(路径,新路径) 则
					刷新或成功事件("文件(夹)重命名成功！",1)
				否则
					刷新或成功事件("重命名失败！",-1)
				结束 如果
				XPopup.关闭()
			结束 如果
		结束 事件
		//关闭
		事件 重命名布局.线性布局7:被单击()
			XPopup.关闭()
		结束 事件
	结束 方法

	//返回类型
	// 0 关闭信息
	// 1 成功
	// -1 错误
	定义事件 刷新或成功事件(返回信息 : 文本,返回类型 : 整数 = 1)

结束 类