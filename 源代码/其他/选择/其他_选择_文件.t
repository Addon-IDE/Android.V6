类 其他_选择_文件 : 组件容器

	@隐藏
	变量 设置_设定 : 设置_设定
	@隐藏
	变量 普通文本框 : 逻辑型 = 真
	@隐藏
	变量 选择对话框 : 对话框
	@隐藏
	变量 对话框布局 : 其他_选择_文件布局
	@隐藏
	变量 列表数组 : 文本集合
	@隐藏
	变量 结束集合 : 文本集合

	@隐藏
	变量 文本框名 : 文本框
	@隐藏
	变量 项目编辑器列表名 : 编辑器_项目编辑器_列表

	@隐藏
	变量 列表文件路径 : 文本
	@隐藏
	变量 活动文件路径 : 文本
	@隐藏
	变量 清除后缀开关 : 逻辑型
	@隐藏
	变量 允许进入文件夹开关 : 逻辑型

	方法 普通文本框(状态 : 逻辑型)
		普通文本框 = 状态
	结束 方法

	属性写 文本框相关(名称 : 文本框)
		文本框名 = 名称
	结束 属性

	属性写 项目编辑器列表相关(名称 : 编辑器_项目编辑器_列表)
		项目编辑器列表名 = 名称
	结束 属性

	属性写 清除后缀(状态 : 逻辑型)
		清除后缀开关 = 状态
	结束 属性

	属性写 允许进入文件夹(状态 : 逻辑型)
		允许进入文件夹开关 = 状态
	结束 属性

	方法 打开(标题名 : 文本,列表路径 : 文本)
		//初始化对话框
		选择对话框.自定义布局 = 对话框布局
		选择对话框.显示()
		//设置相关布局
		对话框布局.文本框1.内容 = 标题名
		对话框布局.文本框4.可用 = 假
		//设置夜间模式
		如果 设置_设定.获取开关("NightMode") 则
			夜间模式()
		结束 如果
		//加载列表
		列表文件路径 = 列表路径
		变量 path : 文本 = 列表文件路径
		如果 文件是否存在(path) 则
			加载列表(path)
		否则
			写出文本文件(path + "null","")
			删除文件(path + "null")
			加载列表(path)
		结束 如果
		//关闭对话框事件
		事件 对话框布局.线性布局10:被单击()
			选择对话框.关闭()
		结束 事件
	结束 方法

	@隐藏
	方法 加载列表(路径 : 文本,刷新 : 逻辑型 = 假)
		活动文件路径 = 路径
		如果 刷新 则
			列表数组 = 文件操作.取子文件集合(路径)
			对话框布局.自定义列表框1.更新项目(列表数组.长度)
		否则
			事件 对话框布局.自定义列表框1:加载布局(索引 : 整数,项目布局 : 组件容器) : 组件容器
				变量 布局 : 其他_选择_文件列表?
				如果 项目布局 == 空 则
					布局 = 创建 其他_选择_文件列表
				否则
					布局 = 项目布局
				结束 如果
				变量 列表数据 : 文本 = 列表数组[索引]
				如果 是否为目录(列表数据) 则
					布局.图片框1.图片资源 = 其他_选择_文件资源.文件夹图标
				否则
					布局.图片框1.图片资源 = 其他_选择_文件资源.文件图标
				结束 如果
				布局.文本框1.内容 = 取文件名(列表数据)
				布局.文本框2.内容 = 取文件名(获取文件父目录(列表数据))
				//列表点击
				事件 布局.线性布局2:被单击()
					对话框布局.文本框4.可用 = 真
					如果 是否为目录(列表数据) 则
						如果 允许进入文件夹开关 则
							加载列表(列表数据,真)
						结束 如果
					否则
						列表被点击(列表数据)
					结束 如果
				结束 事件
				//返回上一级
				事件 对话框布局.文本框4:被单击()
					变量 父目录 : 文本 = 获取文件父目录(活动文件路径)
					如果 列表文件路径 == 活动文件路径+"/" 则
					否则
						加载列表(父目录,真)
					结束 如果
				结束 事件
				返回 布局
			结束 事件
			列表数组 = 文件操作.取子文件集合(路径)
			对话框布局.自定义列表框1.更新项目(列表数组.长度)
		结束 如果
		对话框布局.文本框2.内容 = 列表数组.长度 + " 项"
		//编辑框搜索
		事件 对话框布局.编辑框1:内容被改变()
			列表数组 = 搜索(文件操作.取子文件集合(活动文件路径),对话框布局.编辑框1.内容)
			对话框布局.自定义列表框1.更新项目(列表数组.长度)
		结束 事件
	结束 方法

	方法 列表被点击(路径 : 文本)
		变量 最终路径 : 文本 = 路径.替换(列表文件路径,"")
		变量 清除后缀 : 文本 = 最终路径.替换(".mcfunction","").替换(".MCFUNCTION","")
		如果 取文件后缀名(路径).到小写() == "mcfunction" 则
			如果 普通文本框 则
				如果 清除后缀开关 则
					文本框名.内容 = 清除后缀
				否则
					文本框名.内容 = 最终路径
				结束 如果
			否则
				如果 清除后缀开关 则
					项目编辑器列表名.按钮旁文本内容 = 清除后缀
				否则
					项目编辑器列表名.按钮旁文本内容 = 最终路径
				结束 如果
			结束 如果
			选择对话框.关闭()
		否则
			变量 弹窗提示 : AlertDialog
			弹窗提示.初始化 = 取安卓环境()
			弹窗提示.标题 = "提示"
			弹窗提示.内容 = "你选择的文件为未知后缀，请选择mcfunction的后缀文件"
			弹窗提示.按钮1 = "取消"
			弹窗提示.显示()
		结束 如果
	结束 方法

	方法 获取文件父目录(path : 文本) : 文本
		变量 path_file : 文件 = path
		返回 path_file.取父目录路径()
	结束 方法

	//搜索
	方法 搜索(项目集合 : 文本集合,搜索内容 : 文本) : 文本集合
		结束集合.清空()
		循环(项目集合 -> 项目)
			如果 项目.到文本() ? 搜索内容 则
				结束集合.添加成员(项目)
			结束 如果
		结束 循环
		返回 结束集合
	结束 方法

	方法 夜间模式()
		对话框布局.文本框1.字体颜色 = 0XFFFFFFFF
		对话框布局.线性布局1.背景颜色 = 0XFF222222
		对话框布局.线性布局2.背景颜色 = 0XFF333333
		对话框布局.线性布局3.背景颜色 = 0XFF333333
		对话框布局.线性布局8.背景颜色 = 0XFF222222
		对话框布局.线性布局9.背景颜色 = 0XFF333333
		对话框布局.自定义列表框1.背景颜色 = 0XFF222222
		对话框布局.编辑框1.字体颜色 = 0XFFFFFFFF
		对话框布局.编辑框1.提示文本颜色 = 0XFFE0E0E0
		对话框布局.文本框4.字体颜色 = 0XFFFFFFFF
	结束 方法


结束 类

类 其他_选择_文件布局 : 组件容器

	@布局配置([[根布局=真,宽度=-1,高度=-2,背景颜色=-526087,纵向布局=真]])
	变量 线性布局1 : 线性布局
	@布局配置([[父布局=线性布局1,宽度=-1,高度DP=40,对齐方式=8388627,背景颜色=-1]])
	变量 线性布局2 : 线性布局
	@布局配置([[父布局=线性布局2,@左外边距DP=10,字体大小=16,内容="文件选择",字体颜色=-16777216,@右外边距DP=10]])
	变量 文本框1 : 文本框
	@布局配置([[父布局=线性布局1,宽度=-1,高度=-2,纵向布局=真,背景颜色=-1]])
	变量 线性布局3 : 线性布局
	@布局配置([[父布局=线性布局3,@上外边距DP=0,宽度=-1,高度=-2]])
	变量 线性布局4 : 线性布局
	@布局配置([[父布局=线性布局4,宽度=-1,高度DP=40,对齐方式=17]])
	变量 线性布局6 : 线性布局
	@布局配置([[父布局=线性布局6,宽度DP=40,高度=-1,对齐方式=17,@左外边距DP=10,水波纹效果=真,背景颜色=16777215,内容="↑",字体大小=15,粗体=真,字体颜色=-16777216]])
	变量 文本框4 : 文本框
	@布局配置([[父布局=线性布局6,宽度=-1,背景颜色=0,提示文本="查找搜索...",字体大小=15,内容="",字体颜色=-16777216,@左外边距DP=0,@右外边距DP=10,左内边距DP=5,右内边距DP=5]])
	变量 编辑框1 : 编辑框
	@布局配置([[父布局=线性布局4,宽度=-1,高度DP=40]])
	变量 线性布局7 : 线性布局
	@布局配置([[父布局=线性布局1,宽度=-1,高度=-2,纵向布局=真,背景颜色=-1]])
	变量 线性布局8 : 线性布局
	@布局配置([[父布局=线性布局8,宽度=-1,高度DP=300,@左外边距DP=10,@右外边距DP=10,@下外边距DP=5,分割线高度=0,背景颜色=-526087]])
	变量 自定义列表框1 : 自定义列表框
	@布局配置([[父布局=线性布局8,@左外边距DP=10,宽度=-1,对齐方式=8388629,@右外边距DP=10,字体大小=12,字体颜色=-12410113,内容="0 项",@下外边距DP=5]])
	变量 文本框2 : 文本框
	@布局配置([[父布局=线性布局1,宽度=-1,高度DP=45,对齐方式=8388629,背景颜色=-1]])
	变量 线性布局9 : 线性布局
	@布局配置([[父布局=线性布局9,宽度DP=65,高度DP=30,卡片阴影=0,@右外边距DP=10,卡片背景颜色=0]])
	变量 卡片布局1 : 卡片布局
	@布局配置([[父布局=卡片布局1,宽度=-1,高度=-1,背景颜色=-13781510,对齐方式=17,水波纹效果=真]])
	变量 线性布局10 : 线性布局
	@布局配置([[父布局=线性布局10,字体大小=14,字体颜色=-1,内容="取 消"]])
	变量 文本框3 : 文本框

结束 类

类 其他_选择_文件列表 : 组件容器

	@布局配置([[根布局=真,宽度=-1,高度=-2]])
	变量 线性布局1 : 线性布局
	@布局配置([[父布局=线性布局1,宽度=-1,高度DP=50,圆角=25,卡片阴影=0,@上外边距DP=5]])
	变量 卡片布局1 : 卡片布局
	@布局配置([[父布局=卡片布局1,宽度=-1,高度=-1,背景颜色=-1,水波纹效果=真]])
	变量 线性布局2 : 线性布局
	@布局配置([[父布局=线性布局2,高度=-1,宽度DP=50,对齐方式=17]])
	变量 线性布局3 : 线性布局
	@布局配置([[父布局=线性布局3,宽度DP=35,高度DP=35]])
	变量 图片框1 : 图片框
	@布局配置([[父布局=线性布局2,宽度=-1,高度=-1,对齐方式=8388627,纵向布局=真]])
	变量 线性布局4 : 线性布局
	@布局配置([[父布局=线性布局4,@左外边距DP=5,字体大小=14,字体颜色=-16777216,内容="Name"]])
	变量 文本框1 : 文本框
	@布局配置([[父布局=线性布局4,@左外边距DP=5,字体大小=11,字体颜色=-16777216,内容="父目录"]])
	变量 文本框2 : 文本框

	//声明
	变量 设置_设定 : 设置_设定

	事件 其他_选择_文件列表:创建完毕()
		//设置夜间模式
		如果 设置_设定.获取开关("NightMode") 则
			设置夜间模式()
		结束 如果
	结束 事件

	//设置功能按钮颜色
	方法 设置夜间模式()
		卡片布局1.卡片背景颜色 = 0XFF333333
		线性布局2.背景颜色 = 0XFF333333
		文本框1.字体颜色 = 0XFFFFFFFF
		文本框2.字体颜色 = 0XFFFFFFFF
	结束 方法

结束 类

类 其他_选择_文件资源

	@静态
	变量 文件图标 : 图片资源 = "../../../../资源库/图标/mores/select_file/function.png"

	@静态
	变量 文件夹图标 : 图片资源 = "../../../../资源库/图标/mores/select_file/folder.png"

结束 类