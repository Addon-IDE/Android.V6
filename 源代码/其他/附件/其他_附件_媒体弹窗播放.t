类 其他_附件_媒体播放 : 组件容器

	变量 设置_设定 : 设置_设定

	方法 开始(音频路径 : 文本)
		开始俘获异常()
		变量 对话框 : 对话框
		变量 对话框布局 : 其他_附件_媒体播放布局
		对话框.自定义布局 = 对话框布局
		对话框.可取消 = 假
		对话框.显示()
		//设置夜间模式
		如果 设置_设定.获取开关("NightMode") 则
			对话框布局.线性布局1.背景颜色 = 0XFF333333
			对话框布局.文本框1.字体颜色 = 0XFFFFFFFF
			对话框布局.文本框2.字体颜色 = 0XFFE0E0E0
		结束 如果
		//获取音频名称
		对话框布局.文本框2.内容 = 取文件名(音频路径)
		//初始化圆形进度条
		对话框布局.漂亮的圆形拖动条1.拖动条背景颜色("#00000000")
		对话框布局.漂亮的圆形拖动条1.拖动球颜色("#00000000")
		对话框布局.漂亮的圆形拖动条1.进度条颜色("#ff009dff")
		对话框布局.漂亮的圆形拖动条1.字体大小(0)
		对话框布局.漂亮的圆形拖动条1.字体颜色("#00000000")
		对话框布局.漂亮的圆形拖动条1.是否可拖拽(假)
		对话框布局.漂亮的圆形拖动条1.是否显示外环刻度(真)
		//开始播放
		变量 播放器 : 媒体播放器
		播放器.添加音乐(音频路径)
		播放器.开始()
		对话框布局.漂亮的圆形拖动条1.设置当前进度(0)
		对话框布局.漂亮的圆形拖动条1.设置最大进度(播放器.取音乐总时长())
		对话框布局.拖动条1.最大进度 = 播放器.取音乐总时长()
		//实时获取进度
		提交到新线程运行()
		循环(真)
			延时(800)
			提交到主线程运行(取安卓窗口())
			//实时获取进度
			对话框布局.漂亮的圆形拖动条1.设置当前进度(播放器.取当前播放位置())
			对话框布局.拖动条1.进度 = 播放器.取当前播放位置()
			对话框布局.文本框3.内容 = 时间戳到文本(播放器.取当前播放位置(),"mm:ss")
			结束提交到主线程()
		结束 循环
		结束提交到新线程()
		//启动调节进度
		事件 对话框布局.线性布局8:被单击()
			对话框布局.线性布局11.可视 = 真
			事件 对话框布局.拖动条1:结束拖动()
				播放器.设置进度(对话框布局.拖动条1.进度)
			结束 事件
		结束 事件
		//继续和暂停
		事件 对话框布局.线性布局9:被单击()
			如果 播放器.是否正在播放() 则
				对话框布局.文本框5.内容 = "继续"
				播放器.暂停()
			否则
				对话框布局.文本框5.内容 = "暂停"
				播放器.开始()
			结束 如果
		结束 事件
		//关闭
		事件 对话框布局.线性布局10:被单击()
			播放器.停止()
			对话框.关闭()
		结束 事件
		俘获所有异常()
		变量 异常信息 : 文本 = 取俘获异常().取异常信息()
		变量 错误弹窗 : DX基础对话框
		错误弹窗.标题("播放异常")
		错误弹窗.内容("详细日志：" + 异常信息)
		错误弹窗.按钮1("关闭")
		错误弹窗.按钮2("复制")
		//设置夜间模式
		如果 设置_设定.获取开关("NightMode") 则
			错误弹窗.主题(DX主题.暗色)
		结束 如果
		错误弹窗.显示()
		事件 错误弹窗:按钮2被单击() : 逻辑型
			置剪切板文本(取安卓环境(),异常信息)
			错误弹窗.关闭()
			弹出成功提示("复制完毕！")
			返回 假
		结束 事件
		结束俘获异常()
	结束 方法

结束 类

类 其他_附件_媒体播放布局 : 组件容器

	@布局配置([[根布局=真,宽度=-1,高度=-2,纵向布局=真,背景颜色=-1]])
	变量 线性布局1 : 线性布局
	@布局配置([[父布局=线性布局1,宽度=-1,高度DP=40,对齐方式=8388627]])
	变量 线性布局2 : 线性布局
	@布局配置([[父布局=线性布局2,@左外边距DP=10,字体大小=16,内容="媒体播放器",字体颜色=-16777216]])
	变量 文本框1 : 文本框
	@布局配置([[父布局=线性布局1,宽度=-1,高度DP=30,对齐方式=17]])
	变量 线性布局3 : 线性布局
	@布局配置([[父布局=线性布局3,字体大小=15,内容="音频名称",字体颜色=-16777216]])
	变量 文本框2 : 文本框
	@布局配置([[父布局=线性布局1,宽度=-1,高度DP=150,对齐方式=17]])
	变量 线性布局4 : 线性布局
	@布局配置([[父布局=线性布局4,宽度DP=150,高度DP=150]])
	变量 相对布局1 : 相对布局
	@布局配置([[父布局=相对布局1,宽度=-1,高度=-1]])
	变量 线性布局5 : 线性布局
	@布局配置([[父布局=线性布局5,宽度=-1,高度=-1,可用=假]])
	变量 漂亮的圆形拖动条1 : 漂亮的圆形拖动条
	@布局配置([[父布局=相对布局1,宽度=-1,高度=-1,对齐方式=17]])
	变量 线性布局6 : 线性布局
	@布局配置([[父布局=线性布局6,字体颜色=-12083975,内容="加载中...",字体大小=17,粗体=假,斜体=假]])
	变量 文本框3 : 文本框
	@布局配置([[父布局=线性布局1,宽度=-1,高度DP=25,对齐方式=17,可视=假]])
	变量 线性布局11 : 线性布局
	@布局配置([[父布局=线性布局11,宽度=-1,@左外边距DP=10,@右外边距DP=10]])
	变量 拖动条1 : 拖动条
	@布局配置([[父布局=线性布局1,宽度=-1,高度DP=40,对齐方式=17,@上外边距DP=5,@下外边距DP=15]])
	变量 线性布局7 : 线性布局
	@布局配置([[父布局=线性布局7,高度DP=35,宽度DP=80,背景颜色=-14570766,对齐方式=17]])
	变量 线性布局8 : 线性布局
	@布局配置([[父布局=线性布局8,内容="调节进度",字体大小=15,字体颜色=-1]])
	变量 文本框4 : 文本框
	@布局配置([[父布局=线性布局7,高度DP=35,宽度DP=100,背景颜色=-14570766,@左外边距DP=5,@右外边距DP=5,对齐方式=17]])
	变量 线性布局9 : 线性布局
	@布局配置([[父布局=线性布局9,内容="暂停",字体大小=15,字体颜色=-1]])
	变量 文本框5 : 文本框
	@布局配置([[父布局=线性布局7,高度DP=35,宽度DP=80,背景颜色=-14570766,对齐方式=17]])
	变量 线性布局10 : 线性布局
	@布局配置([[父布局=线性布局10,内容="关闭",字体大小=15,字体颜色=-1]])
	变量 文本框6 : 文本框


结束 类