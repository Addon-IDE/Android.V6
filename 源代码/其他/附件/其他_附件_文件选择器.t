类 其他_附件_文件选择器 : 窗口

	@布局配置([[根布局=真,宽度=-1,高度=-1,纵向布局=真]])
	变量 线性布局1 : 线性布局
	@布局配置([[父布局=线性布局1,宽度=-1,高度DP=50,对齐方式=8388627,背景颜色=-1]])
	变量 线性布局2 : 线性布局
	@布局配置([[父布局=线性布局2,宽度DP=15,高度DP=15,图片资源="../../../../资源库/图标/back/Back_black.png",@左外边距DP=10]])
	变量 图片框1 : 图片框
	@布局配置([[父布局=线性布局2,字体大小=15,内容="项目",字体颜色=-16777216]])
	变量 文本框2 : 文本框
	@布局配置([[父布局=线性布局2,宽度DP=2,高度DP=12,@左外边距DP=10,卡片阴影=0]])
	变量 卡片布局1 : 卡片布局
	@布局配置([[父布局=卡片布局1,宽度=-1,高度=-1,背景颜色=-526087]])
	变量 线性布局3 : 线性布局
	@布局配置([[父布局=线性布局2,@左外边距DP=10,粗体=真,字体大小=16,内容="选择文件(*.json)",字体颜色=-16777216]])
	变量 文本框3 : 文本框
	@布局配置([[父布局=线性布局1,宽度=-1,高度=-1,纵向布局=真]])
	变量 线性布局4 : 线性布局
	@布局配置([[父布局=线性布局4,宽度=-1,高度DP=20,对齐方式=8388627]])
	变量 线性布局7 : 线性布局
	@布局配置([[父布局=线性布局7,@左外边距DP=10,字体大小=12,字体颜色=-16777216]])
	变量 文本框5 : 文本框
	@布局配置([[父布局=线性布局4,宽度=-1,高度DP=40,水波纹效果=真,对齐方式=8388627]])
	变量 线性布局5 : 线性布局
	@布局配置([[父布局=线性布局5,宽度DP=28,高度DP=28,@左外边距DP=10,图片资源="../../../../资源库/图标/mores/file_selector/collection.png"]])
	变量 图片框2 : 图片框
	@布局配置([[父布局=线性布局5,@左外边距DP=5,字体大小=15,字体颜色=-16777216,内容="收藏"]])
	变量 文本框1 : 文本框
	@布局配置([[父布局=线性布局4,宽度=-1,高度DP=40,水波纹效果=真,对齐方式=8388627]])
	变量 线性布局6 : 线性布局
	@布局配置([[父布局=线性布局6,宽度DP=25,高度DP=25,@左外边距DP=10,图片资源="../../../../资源库/图标/mores/file_selector/superior_level.png"]])
	变量 图片框4 : 图片框
	@布局配置([[父布局=线性布局6,@左外边距DP=5,字体大小=15,字体颜色=-16777216,内容="上一级"]])
	变量 文本框4 : 文本框
	@布局配置([[父布局=线性布局4,宽度=-1,高度=-1,始终显示快速滚动条=真,分割线高度=0]])
	变量 自定义列表框1 : 自定义列表框

	//声明
	变量 设置_设定 : 设置_设定
	变量 列表集合 : 文本集合
	变量 列表数组 : 文本集合

	//变量
	变量 文件分类 : 文本 = ""
	变量 活动路径 : 文本 = ""

	事件 其他_附件_文件选择器:创建完毕()
		//设置主题
		本对象.显示标题栏 = 假
		本对象.状态栏颜色 = 0XFFFFFFFF
		本对象.状态栏字体黑色 = 真
		//窗口切换动画
		播放默认切换动画(2)
		//退出按钮
		事件 图片框1:被单击()
			关闭窗口()
		结束 事件
		事件 文本框2:被单击()
			关闭窗口()
		结束 事件
		//设置夜间模式
		如果 设置_设定.获取开关("NightMode") 则
			设置夜间模式()
		结束 如果
		//设置背景
		变量 设置相关路径 : 文本 = 设置_设定.获取文本("PersonalityBackground")
		如果 设置相关路径.替换(" ","") == "" 则
		否则
			如果 文件是否存在(设置相关路径) 则
				设置_设置.设置布局背景(本对象,线性布局4,设置相关路径)
			结束 如果
		结束 如果
		//设置文件类型
		文件分类 = 其他_其他.读缓存("file_selector_type")
		//加载方法
		列表加载(其他_变量.内部存储目录)
		//设置标题
		文本框3.内容 = "选择文件(*." + 文件分类 + ")"
		//设置活动路径，用于加载上一级相关功能
		活动路径 = 取存储卡路径()
		//检查收藏文件是否存在
		变量 收藏文件 : 文本 = 其他_变量.数据目录 + "mores/file_selector_collection.json"
		如果 文件是否存在(收藏文件) 则
		否则
			写出资源文件(本对象,"mores/file_selector_collection.json",收藏文件)
		结束 如果
		//订阅事件
		订阅事件()
	结束 事件

	方法 设置文件类型(类型 : 文本,返回值 : 整数)
		其他_其他.写缓存("file_selector_type",类型)
		其他_其他.写缓存("file_selector_exts",返回值.到文本())
	结束 方法

	方法 列表加载(路径 : 文本,刷新 : 逻辑型 = 假)
		活动路径 = 路径
		文本框5.内容 = 活动路径
		如果 刷新 则
			列表集合 = 文件操作.取子文件集合(路径)
			自定义列表框1.更新项目(列表集合.长度)
		否则
			事件 自定义列表框1:加载布局(索引 : 整数,项目布局 : 组件容器) : 组件容器
				变量 布局 : 其他_附件_文件选择器_列表?
				如果 项目布局 == 空 则
					布局 = 创建 其他_附件_文件选择器_列表
				否则
					布局 = 项目布局
				结束 如果
				//显示到列表
				变量 文件路径 : 文本 = 列表集合[索引]
				如果 是否为目录(文件路径) 则
					布局.图片框1.图片资源 = 其他_附件_文件选择器_资源.文件夹
					布局.文本框1.内容 = 取文件名(文件路径)
				否则
					如果 取文件后缀名(文件路径).到小写() == 文件分类.到小写() 则
						布局.图片框1.图片资源 = 其他_附件_文件选择器_资源.文件
						布局.文本框1.内容 = 取文件名(文件路径)
						布局.线性布局1.可视 = 真
					否则
						布局.线性布局1.可视 = 假
					结束 如果
				结束 如果
				//列表被点击
				事件 布局.相对布局1:被单击()
					如果 是否为目录(文件路径) 则
						列表加载(文件路径,真)
					否则
						如果 文件是否存在(文件路径) 则
							其他_其他.写缓存("file_selector_path",文件路径)
							其他_其他.写缓存("file_selector_true","1")
							关闭窗口()
						结束 如果
					结束 如果
				结束 事件
				//列表被长按
				事件 布局.相对布局1:被长按()
					如果 是否为目录(文件路径) 则
						添加收藏(文件路径)
					否则
						变量 wj : 文件 = 文件路径
						变量 flj : 文本 = wj.取父目录路径()
						添加收藏(flj)
					结束 如果
				结束 事件
				返回 布局
			结束 事件
			列表集合 = 文件操作.取子文件集合(路径)
			自定义列表框1.更新项目(列表集合.长度)
		结束 如果
	结束 方法

	//收藏
	事件 线性布局5:被单击()
		//对话框
		变量 收藏对话框 : XPopup自定义弹窗
		变量 对话布局 : 其他_附件_文件选择器_收藏
		收藏对话框.布局(对话布局)
		收藏对话框.高斯模糊背景(真)
		收藏对话框.动画(XPopup动画.缩放_中心)
		收藏对话框.显示()
		//夜间模式
		如果 设置_设定.获取开关("NightMode") 则
			对话布局.卡片布局1.卡片背景颜色 = 0XFF333333
			对话布局.线性布局2.背景颜色 = 0XFF333333
			对话布局.文本框1.字体颜色 = 0XFFFFFFFF
			对话布局.文本框2.字体颜色 = 0XFFFFFFFF
		结束 如果
		//加载列表
		变量 收藏文件 : 文本 = 其他_变量.数据目录 + "mores/file_selector_collection.json"
		变量 数组内容 : JSON数组 = 读入文本文件(收藏文件)
		列表数组.清空()
		循环(i, 0, 数组内容.长度)
			列表数组.添加成员(数组内容.取文本(i).到文本())
		结束 循环
		事件 对话布局.自定义列表框1:加载布局(索引 : 整数,项目布局 : 组件容器) : 组件容器
			变量 布局 : 其他_附件_文件选择器_列表?
			如果 项目布局 == 空 则
				布局 = 创建 其他_附件_文件选择器_列表
			否则
				布局 = 项目布局
			结束 如果
			//设置列表内容
			变量 列表数据内容 : 文本 = 列表数组[索引]
			变量 json : JSON对象 = 列表数据内容
			布局.文本框2.可视 = 真
			布局.图片框1.图片资源 = 其他_附件_文件选择器_资源.文件夹
			布局.文本框1.内容 = 取文件名(json["path"].到文本())
			布局.文本框2.内容 = json["describe"].到文本()
			//列表被点击
			事件 布局.相对布局1:被单击()
				变量 路径s : 文本 = json["path"].到文本().替换("%sdcard%",取存储卡路径())
				如果 文件是否存在(路径s) 则
					列表加载(路径s,真)
				否则
					弹出错误提示("此目录不存在！")
				结束 如果
				收藏对话框.关闭()
			结束 事件
			//列表被长按
			事件 布局.相对布局1:被长按()
				收藏对话框.关闭()
				变量 删除提示 : DX基础对话框
				删除提示.标题("提示")
				删除提示.内容("是否删除此收藏")
				如果 设置_设定.获取开关("NightMode") 则
					删除提示.主题(DX主题.暗色)
				结束 如果
				删除提示.样式(DX样式.IOS)
				删除提示.按钮1("确定")
				删除提示.按钮2("取消")
				删除提示.显示()
				事件 删除提示:按钮1被单击() : 逻辑型
					变量 收藏文件 : 文本 = 其他_变量.数据目录 + "mores/file_selector_collection.json"
					变量 数组内容 : JSON数组 = 读入文本文件(收藏文件)
					数组内容.移除(索引)
					写出文本文件(收藏文件,数组内容.到文本(4))
					删除提示.关闭()
					弹出成功提示("删除完毕！")
					返回 假
				结束 事件
			结束 事件
			返回 布局
		结束 事件
		对话布局.自定义列表框1.更新项目(列表数组.长度)
		//关闭对话框
		事件 对话布局.文本框2:被单击()
			收藏对话框.关闭()
		结束 事件
	结束 事件

	方法 添加收藏(路径 : 文本)
		变量 新建收藏对话框 : DX输入对话框
		新建收藏对话框.提示文本("请输入收藏的描述...")
		新建收藏对话框.标题("添加收藏")
		如果 设置_设定.获取开关("NightMode") 则
			新建收藏对话框.主题(DX主题.暗色)
		结束 如果
		新建收藏对话框.样式(DX样式.IOS)
		新建收藏对话框.按钮1("添加")
		新建收藏对话框.按钮2("取消")
		新建收藏对话框.显示()
		事件 新建收藏对话框:按钮1被单击(文本 : 文本) : 逻辑型
			变量 收藏文件 : 文本 = 其他_变量.数据目录 + "mores/file_selector_collection.json"
			变量 数组内容 : JSON数组 = 读入文本文件(收藏文件)
			变量 json : JSON对象
			json["path"] = 路径
			json["describe"] = 文本
			数组内容.置入(json)
			写出文本文件(收藏文件,数组内容.到文本(4))
			新建收藏对话框.关闭()
			返回 假
		结束 事件
	结束 方法

	//按钮上一级
	事件 线性布局6:被单击()
		如果 活动路径 == 取存储卡路径() 则
		否则
			变量 wj : 文件 = 活动路径
			变量 flj : 文本 = wj.取父目录路径()
			活动路径 = flj
			列表加载(flj,真)
		结束 如果
	结束 事件

	//设置功能按钮颜色
	方法 设置夜间模式()
		本对象.状态栏颜色 = 0XFF333333
		本对象.状态栏字体黑色 = 假
		线性布局2.背景颜色 = 0XFF333333
		线性布局3.背景颜色 = 0XFF222222
		卡片布局1.卡片背景颜色 = 0XFF222222
		文本框2.字体颜色 = 0XFFFFFFFF
		文本框3.字体颜色 = 0XFFFFFFFF
		图片框1.图片资源 = 其他_资源.白色返回图标
		文本框1.字体颜色 = 0XFFFFFFFF
		文本框5.字体颜色 = 0XFFFFFFFF
		文本框4.字体颜色 = 0XFFFFFFFF
		线性布局4.背景颜色 = 0XFF333333
	结束 方法

结束 类

类 其他_附件_文件选择器_收藏 : 组件容器

	@布局配置([[根布局=真,宽度=-1,高度=-1,对齐方式=17]])
	变量 线性布局1 : 线性布局
	@布局配置([[父布局=线性布局1,宽度DP=280,高度DP=400,圆角=25,卡片阴影=0]])
	变量 卡片布局1 : 卡片布局
	@布局配置([[父布局=卡片布局1,宽度=-1,高度=-1,背景颜色=-1,纵向布局=真]])
	变量 线性布局2 : 线性布局
	@布局配置([[父布局=线性布局2,宽度=-1,高度DP=30,对齐方式=8388627]])
	变量 线性布局3 : 线性布局
	@布局配置([[父布局=线性布局3,@左外边距DP=10,字体颜色=-16777216,粗体=真,内容="收藏"]])
	变量 文本框1 : 文本框
	@布局配置([[父布局=线性布局3,宽度=-1,对齐方式=8388629,@左外边距DP=0,@右外边距DP=10,字体颜色=-16777216,斜体=假,粗体=真,内容="关闭"]])
	变量 文本框2 : 文本框
	@布局配置([[父布局=线性布局2,宽度=-1,高度=-1,始终显示快速滚动条=真,分割线高度=0]])
	变量 自定义列表框1 : 自定义列表框


结束 类

类 其他_附件_文件选择器_列表 : 组件容器

	@布局配置([[根布局=真,宽度=-1,高度=-2,水波纹效果=真]])
	变量 线性布局1 : 线性布局
	@布局配置([[父布局=线性布局1,宽度=-1,高度DP=45,背景颜色=-1,水波纹效果=真]])
	变量 相对布局1 : 相对布局
	@布局配置([[父布局=相对布局1,宽度=-1,高度=-1,对齐方式=8388627]])
	变量 线性布局2 : 线性布局
	@布局配置([[父布局=线性布局2,高度DP=45,宽度DP=45,对齐方式=17]])
	变量 线性布局3 : 线性布局
	@布局配置([[父布局=线性布局3,宽度DP=30,高度DP=30]])
	变量 图片框1 : 图片框
	@布局配置([[父布局=线性布局2,宽度=-1,高度=-1,对齐方式=8388627,纵向布局=真]])
	变量 线性布局4 : 线性布局
	@布局配置([[父布局=线性布局4,@左外边距DP=5,字体颜色=-16777216,字体大小=14]])
	变量 文本框1 : 文本框
	@布局配置([[父布局=线性布局4,@左外边距DP=5,字体颜色=-16777216,字体大小=11,可视=假]])
	变量 文本框2 : 文本框

	//声明
	变量 设置_设定 : 设置_设定

	事件 其他_附件_文件选择器_列表:创建完毕()
		//设置夜间模式
		如果 设置_设定.获取开关("NightMode") 则
			设置夜间模式()
		结束 如果
	结束 事件

	//设置夜间模式
	方法 设置夜间模式()
		相对布局1.背景颜色 = 0XFF333333
		文本框1.字体颜色 = 0XFFFFFFFF
		文本框2.字体颜色 = 0XFFFFFFFF
	结束 方法

结束 类

类 其他_附件_文件选择器_资源

	@静态
	变量 文件 : 图片资源 = "../../../../资源库/图标/mores/file_selector/file.png"

	@静态
	变量 文件夹 : 图片资源 = "../../../../资源库/图标/mores/file_selector/folder.png"

结束 类