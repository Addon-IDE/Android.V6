类 其他_管理_标识管理器 : 窗口

	@布局配置([[根布局=真,宽度=-1,高度=-1,纵向布局=真]])
	变量 线性布局1 : 线性布局
	@布局配置([[父布局=线性布局1,宽度=-1,高度DP=50,对齐方式=8388627,背景颜色=-1]])
	变量 线性布局2 : 线性布局
	@布局配置([[父布局=线性布局2,宽度DP=15,高度DP=15,图片资源="../../../../资源库/图标/back/Back_black.png",@左外边距DP=10]])
	变量 图片框1 : 图片框
	@布局配置([[父布局=线性布局2,字体大小=15,内容="项目",字体颜色=-16777216]])
	变量 文本框2 : 文本框
	@布局配置([[父布局=线性布局2,宽度DP=2,高度DP=12,@左外边距DP=10,卡片阴影=0]])
	变量 卡片布局1 : 卡片布局
	@布局配置([[父布局=卡片布局1,宽度=-1,高度=-1,背景颜色=-526087]])
	变量 线性布局3 : 线性布局
	@布局配置([[父布局=线性布局2,@左外边距DP=10,粗体=真,字体大小=16,内容="ID 管理器",字体颜色=-16777216]])
	变量 文本框3 : 文本框
	@布局配置([[父布局=线性布局2,宽度=-1,@左外边距DP=10,@右外边距DP=10,背景颜色=16777215,左内边距DP=5,右内边距DP=5,字体大小=16,内容="",提示文本="查找内容..."]])
	变量 编辑框1 : 编辑框
	@布局配置([[父布局=线性布局1,宽度=-1,高度=-1]])
	变量 相对布局1 : 相对布局
	@布局配置([[父布局=相对布局1,宽度=-1,高度=-1]])
	变量 线性布局6 : 线性布局
	@布局配置([[父布局=线性布局6,宽度=-1,高度=-1,始终显示快速滚动条=真,分割线高度=0]])
	变量 自定义列表框1 : 自定义列表框
	@布局配置([[父布局=线性布局6,宽度=-1,高度=-1,始终显示快速滚动条=真,分割线高度=0,可视=假]])
	变量 自定义列表框2 : 自定义列表框
	@布局配置([[父布局=相对布局1,宽度=-1,高度=-1,对齐方式=8388693]])
	变量 线性布局4 : 线性布局
	@布局配置([[父布局=线性布局4,宽度DP=55,高度DP=55,圆角=1000,卡片阴影=0,@下外边距DP=50,@右外边距DP=50]])
	变量 卡片布局2 : 卡片布局
	@布局配置([[父布局=卡片布局2,宽度=-1,高度=-1,对齐方式=17,背景颜色=-1222568]])
	变量 线性布局5 : 线性布局
	@布局配置([[父布局=线性布局5,宽度DP=25,高度DP=25,图片资源="../../../../资源库/图标/mores/identifier_manager/add.png"]])
	变量 图片框2 : 图片框

	//声明
	变量 设置_设定 : 设置_设定
	变量 列表数组 : 文本集合
	变量 结束集合 : 文本集合
	变量 文件选择器 : 文件选择器

	//变量
	变量 列表 : 整数 = 0
	变量 文件名 : 文本 = "null"
	变量 图片位置 : 文本
	变量 项目文件路径 : 文本
	变量 操作布局名称 : 其他_管理_标识管理器_操作

	事件 其他_管理_标识管理器:创建完毕()
		//设置主题
		本对象.显示标题栏 = 假
		本对象.状态栏颜色 = 0XFFFFFFFF
		本对象.状态栏字体黑色 = 真
		//窗口切换动画
		播放默认切换动画(2)
		//退出按钮
		事件 图片框1:被单击()
			关闭窗口()
		结束 事件
		事件 文本框2:被单击()
			关闭窗口()
		结束 事件
		//加载方法
		变量 文件路径列表 : 文本 = 其他_变量.数据目录 + "identifier/"
		如果 文件是否存在(文件路径列表) 则
			加载列表(文件路径列表)
		否则
			写出文本文件(文件路径列表 + "null","")
			删除文件(文件路径列表 + "null")
			加载列表(文件路径列表)
		结束 如果
		//设置夜间模式
		如果 设置_设定.获取开关("NightMode") 则
			设置夜间模式()
		结束 如果
		//设置背景
		变量 设置相关路径 : 文本 = 设置_设定.获取文本("PersonalityBackground")
		如果 设置相关路径.替换(" ","") == "" 则
		否则
			如果 文件是否存在(设置相关路径) 则
				设置_设置.设置布局背景(本对象,自定义列表框1,设置相关路径)
			结束 如果
		结束 如果
		//订阅事件
		订阅事件()
	结束 事件

	//创建
	事件 线性布局5:被单击()
		新建()
	结束 事件

	@虚拟方法
	方法 返回键被按下()
		//退出
		如果 列表 == 0 则
			关闭窗口()
		结束 如果
		//清除编辑框
		编辑框1.内容 = ""
		//设置列表
		列表 = 0
		//重新刷新列表
		变量 文件路径列表 : 文本 = 其他_变量.数据目录 + "identifier/"
		如果 文件是否存在(文件路径列表) 则
			加载列表(文件路径列表)
		否则
			写出文本文件(文件路径列表 + "null","")
			删除文件(文件路径列表 + "null")
			加载列表(文件路径列表)
		结束 如果
		//显示出文件列表
		如果 列表 == 0 则
			自定义列表框1.可视 = 真
			自定义列表框2.可视 = 假
		结束 如果
	结束 方法

	方法 获取标题(ID : 文本) : 文本
		变量 json2name : 文本 = ""
		变量 json : JSON对象 = 读入资源文件(本对象,"data/identifier.json")
		变量 json_name : JSON对象 = json["name"]
		如果 json_name.是否存在(ID) 则
			json2name = json_name[ID]
		否则
			json2name = ""
		结束 如果
		返回 json2name
	结束 方法

	方法 加载列表(路径 : 文本,刷新 : 逻辑型 = 假)
		如果 文件是否存在(路径) 则
			如果 刷新 则
				列表数组 = 文件操作.取子文件集合(路径)
				自定义列表框1.更新项目(列表数组.长度)
			否则 
				事件 自定义列表框1:加载布局(索引 : 整数,项目布局 : 组件容器) : 组件容器
					变量 布局 : 其他_管理_标识管理器_列表?
					如果 项目布局 == 空 则
						布局 = 创建 其他_管理_标识管理器_列表
					否则
						布局 = 项目布局
					结束 如果
					//设置列表内容
					变量 文件路径 : 文本 = 列表数组[索引]
					布局.图片框1.图片资源 = 其他_管理_标识管理器_资源.列表图标
					布局.文本框1.内容 = 取文件名(文件路径)
					布局.文本框2.内容 = 获取标题(取文件前缀名(文件路径))
					布局.文本框3.可视 = 假
					//将文件夹隐藏
					如果 是否为目录(文件路径) 则
						布局.线性布局1.可视 = 假
					否则
						布局.线性布局1.可视 = 真
					结束 如果
					//列表被单击
					事件 布局.线性布局1:被单击()
						列表 = 1
						编辑框1.内容 = ""
						文件名 = 取文件前缀名(文件路径)
						项目文件路径 = 文件路径
						变量 JsonList : JSON数组 = 读入文本文件(文件路径)
						加载JSON列表(JsonList)
						自定义列表框1.可视 = 假
						自定义列表框2.可视 = 真
					结束 事件
					返回 布局
				结束 事件
				列表数组 = 文件操作.取子文件集合(路径)
				自定义列表框1.更新项目(列表数组.长度)
			结束 如果
		否则
			写出文本文件(路径 + "null","")
			删除文件(路径 + "null")
			加载列表(路径)
		结束 如果
		//编辑框搜索
		事件 编辑框1:内容被改变()
			如果 列表 == 0 则
				列表数组 = 搜索(文件操作.取子文件集合(路径),编辑框1.内容)
				自定义列表框1.更新项目(列表数组.长度)
			结束 如果
		结束 事件
	结束 方法

	方法 加载JSON列表(数组内容 : JSON数组)
		//初始化
		列表数组.清空()
		//循环
		循环(i, 0, 数组内容.长度)
			列表数组.添加成员(数组内容.取文本(i).到文本())
		结束 循环
		//加载列表
		事件 自定义列表框2:加载布局(索引 : 整数,项目布局 : 组件容器) : 组件容器
			变量 布局 : 其他_管理_标识管理器_列表?
			如果 项目布局 == 空 则
				布局 = 创建 其他_管理_标识管理器_列表
			否则
				布局 = 项目布局
			结束 如果
			//设置列表内容
			变量 列表数据内容 : 文本 = 列表数组[索引]
			变量 json : JSON对象 = 列表数据内容
			变量 图片路径 : 文本 = 其他_变量.数据目录 + "identifier/icons/" + 文件名 + "/"
			布局.图片框1.图片路径 = 图片路径 + json["icon"].到文本()
			布局.文本框1.内容 = json["name"].到文本()
			布局.文本框2.内容 = json["id"].到文本()
			如果 json.是否存在("data") 则
				布局.文本框3.可视 = 真
				布局.文本框3.内容 = json["data"].到文本()
			否则
				布局.文本框3.可视 = 假
			结束 如果
			//列表被单击
			事件 布局.线性布局2:被单击()
				编辑(索引)
			结束 事件
			//列表被长按
			事件 布局.线性布局2:被长按()
				变量 XPopup : XPopup附着弹窗
				变量 项目 : 文本[] = {"删除"}
				XPopup.列表项目 = 项目
				XPopup.附着组件 = 布局.线性布局2
				XPopup.宽度(500)
				XPopup.亮色状态栏(真)
				XPopup.动画(XPopup动画.滑动_上)
				XPopup.半透明(假)
				//设置夜间模式
				如果 设置_设定.获取开关("NightMode") 则
					XPopup.暗色主题(真)
				结束 如果
				XPopup.显示()
				//选项被单击
				事件 XPopup:列表被单击(索引s : 整数,文本 : 文本)
					假如 索引s
						是 0
							变量 删除对话框 : XPopup对话弹窗
							删除对话框.标题 = "提示"
							删除对话框.内容 = "是否删除这个值"
							如果 设置_设定.获取开关("NightMode") 则
								删除对话框.暗色主题(真)
							结束 如果
							删除对话框.显示()
							事件 删除对话框:确定()
								如果 文件是否存在(项目文件路径) 则
									变量 JsonList : JSON数组 = 读入文本文件(项目文件路径)
									JsonList.移除(索引)
									写出文本文件(项目文件路径,JsonList.到文本(4))
									加载JSON列表(JsonList)
									弹出成功通知("删除成功！")
								否则
									弹出错误通知("删除失败，文件不存在！")
								结束 如果
							结束 事件
					结束 假如
				结束 事件
			结束 事件
			返回 布局
		结束 事件
		//更新列表
		自定义列表框2.更新项目(列表数组.长度)
		//搜索列表
		事件 编辑框1:内容被改变()
			如果 列表 == 1 则
				列表数组.清空()
				循环(i, 0, 数组内容.长度)
					如果 数组内容[i].到文本() ? 编辑框1.内容 则
						列表数组.添加成员(数组内容[i].到文本())
					结束 如果
				结束 循环
				自定义列表框2.更新项目(列表数组.长度)
			结束 如果
		结束 事件
	结束 方法

	方法 新建(datakg : 逻辑型 = 假)
		如果 列表 == 1 则
			变量 操作布局 : 其他_管理_标识管理器_操作
			变量 创建对话框 : XPopup自定义弹窗
			创建对话框.布局(操作布局)
			创建对话框.动画(XPopup动画.滑动_上)
			创建对话框.显示()
			操作布局名称 = 操作布局
			//防止自动弹出输入法
			操作布局.文本框防止自动弹出输入法.跑马灯效果 = 真
			操作布局.文本框防止自动弹出输入法.请求焦点()
			//夜间模式
			如果 设置_设定.获取开关("NightMode") 则
				设置对话框夜间模式(操作布局)
			结束 如果
			//图片选择
			事件 操作布局.线性布局3:被单击()
				文件选择器.选择文件(本对象,1000)
			结束 事件
			//设置
			如果 datakg 则
				操作布局.编辑框3.可视 = 真
			否则
				操作布局.编辑框3.可视 = 假
			结束 如果
			//确定
			事件 操作布局.线性布局14:被单击()
				变量 名称 : 文本 = 操作布局.编辑框1.内容
				变量 ID : 文本 = 操作布局.编辑框2.内容
				变量 DATA : 文本 = 操作布局.编辑框3.内容
				变量 获得文件路径 : 文本 = 操作布局.图标路径.内容
				变量 图片输出路径 : 文本 = 其他_变量.数据目录 + "identifier/icons/" + 文件名 + "/"
				//icon
				如果 操作布局.图标路径.内容 == "Xinyinglovesyou" 则
					写出资源文件(本对象,"icon/identifier/item_id.png",图片输出路径 + ID + ".png")
				否则
					复制文件(获得文件路径,图片输出路径 + ID + ".png")
				结束 如果
				//json
				如果 文件是否存在(项目文件路径) 则
					变量 JsonList : JSON数组 = 读入文本文件(项目文件路径)
					变量 jsons : JSON对象
					jsons["icon"] = ID + ".png"
					jsons["name"] = 名称
					jsons["id"] = ID
					如果 datakg 则
						jsons["data"] = DATA.到整数()
					结束 如果
					JsonList.置入(jsons)
					写出文本文件(项目文件路径,JsonList.到文本(4))
					加载JSON列表(JsonList)
					弹出成功通知("创建成功！")
				否则
					弹出错误通知("创建失败，文件不存在！")
				结束 如果
				创建对话框.关闭()
			结束 事件
			//取消
			事件 操作布局.线性布局15:被单击()
				创建对话框.关闭()
			结束 事件
		否则
			弹出警告通知("此区域不支持新建！")
		结束 如果
	结束 方法

	方法 编辑(索引 : 整数)
		如果 列表 == 1 则
			变量 操作布局 : 其他_管理_标识管理器_操作
			变量 编辑对话框 : XPopup自定义弹窗
			编辑对话框.布局(操作布局)
			编辑对话框.动画(XPopup动画.滑动_上)
			编辑对话框.显示()
			操作布局名称 = 操作布局
			//夜间模式
			如果 设置_设定.获取开关("NightMode") 则
				设置对话框夜间模式(操作布局)
			结束 如果
			//设置
			变量 图片路径 : 文本 = 其他_变量.数据目录 + "identifier/icons/" + 文件名 + "/"
			变量 JsonList : JSON数组 = 读入文本文件(项目文件路径)
			变量 json : JSON对象 = JsonList[索引]
			操作布局.图片框1.图片路径 = 图片路径 + json["icon"].到文本()
			操作布局.图标路径.内容 = 图片路径 + json["icon"].到文本()
			操作布局.编辑框1.内容 = json["name"].到文本()
			操作布局.编辑框2.内容 = json["id"].到文本()
			如果 json.是否存在("data") 则
				操作布局.编辑框3.可视 = 真
				操作布局.编辑框3.内容 = json["data"].到文本()
			否则
				操作布局.编辑框3.可视 = 假
			结束 如果
			//图片选择
			事件 操作布局.线性布局3:被单击()
				文件选择器.选择文件(本对象,1000)
			结束 事件
			//确定
			事件 操作布局.线性布局14:被单击()
				变量 名称 : 文本 = 操作布局.编辑框1.内容
				变量 ID : 文本 = 操作布局.编辑框2.内容
				变量 DATA : 文本 = 操作布局.编辑框3.内容
				变量 获得文件路径 : 文本 = 操作布局.图标路径.内容
				变量 图片输出路径 : 文本 = 其他_变量.数据目录 + "identifier/icons/" + 文件名 + "/"
				//icon
				如果 操作布局.图标路径.内容 == 图片路径 + json["icon"].到文本() 则
					变量 原ID名 : 文本 = json["id"].到文本()
					重命名文件(图片输出路径 + 原ID名 + ".png",图片输出路径 + ID + ".png")
				否则
					复制文件(获得文件路径,图片输出路径 + ID + ".png")
				结束 如果
				//json
				如果 文件是否存在(项目文件路径) 则
					变量 jsons : JSON对象
					jsons["icon"] = ID + ".png"
					jsons["name"] = 名称
					jsons["id"] = ID
					如果 json.是否存在("data") 则
						jsons["data"] = DATA.到整数()
					结束 如果
					JsonList[索引] = jsons
					写出文本文件(项目文件路径,JsonList.到文本(4))
					加载JSON列表(JsonList)
					弹出成功通知("修改成功！")
				否则
					弹出错误通知("修改失败，文件不存在！")
				结束 如果
				编辑对话框.关闭()
			结束 事件
			//取消
			事件 操作布局.线性布局15:被单击()
				编辑对话框.关闭()
			结束 事件
		否则
			弹出警告通知("此区域不支持修改！")
		结束 如果
	结束 方法

	//回调事件
	事件 其他_管理_标识管理器:获得返回数据(请求码 : 整数,结果码 : 整数,数据 : 启动信息)
		假如 请求码
			是 1000
				图片位置 = 文件选择器.解析文件地址(本对象,数据)
				变量 取后缀 : 文本 = 取文件后缀名(图片位置).到小写()
				如果 取后缀 == "png" || 取后缀 == "jpg" 则
					操作布局名称.图片框1.图片路径 = 图片位置
					操作布局名称.图标路径.内容 = 图片位置
				否则
					弹出警告提示("你选择的文件不是图片")
				结束 如果
		结束 假如
	结束 事件

	方法 设置对话框夜间模式(操作布局 : 其他_管理_标识管理器_操作)
		操作布局.线性布局2.背景颜色 = 0XFF333333
		操作布局.线性布局4.背景颜色 = 0XFF333333
		操作布局.线性布局14.背景颜色 = 0XFF333333
		操作布局.线性布局15.背景颜色 = 0XFF333333
		操作布局.文本框1.字体颜色 = 0XFFFFFFFF
		操作布局.文本框2.字体颜色 = 0XFFFFFFFF
		操作布局.文本框3.字体颜色 = 0XFFFFFFFF
		操作布局.文本框4.字体颜色 = 0XFFFFFFFF
		操作布局.文本框5.字体颜色 = 0XFFFFFFFF
		操作布局.卡片布局1.卡片背景颜色 = 0XFF222222
		操作布局.线性布局3.背景颜色 = 0XFF222222
		操作布局.编辑框1.字体颜色 = 0XFFFFFFFF
		操作布局.编辑框2.字体颜色 = 0XFFFFFFFF
		操作布局.编辑框3.字体颜色 = 0XFFFFFFFF
		操作布局.编辑框1.提示文本颜色 = 0XFFB6B6B6
		操作布局.编辑框2.提示文本颜色 = 0XFFB6B6B6
		操作布局.编辑框3.提示文本颜色 = 0XFFB6B6B6
		操作布局.线性布局7.背景颜色 = 0XFF222222
		操作布局.线性布局13.背景颜色 = 0XFF222222
		操作布局.线性布局12.背景颜色 = 0XFF222222
	结束 方法

	//搜索
	方法 搜索(项目集合 : 文本集合,搜索内容 : 文本) : 文本集合
		结束集合.清空()
		循环(项目集合 -> 项目)
			如果 项目.到文本() ? 搜索内容 则
				结束集合.添加成员(项目)
			结束 如果
		结束 循环
		返回 结束集合
	结束 方法

	//设置功能按钮颜色
	方法 设置夜间模式()
		本对象.状态栏颜色 = 0XFF333333
		本对象.状态栏字体黑色 = 假
		线性布局2.背景颜色 = 0XFF333333
		线性布局3.背景颜色 = 0XFF222222
		卡片布局1.卡片背景颜色 = 0XFF222222
		文本框2.字体颜色 = 0XFFFFFFFF
		文本框3.字体颜色 = 0XFFFFFFFF
		编辑框1.字体颜色 = 0XFFFFFFFF
		编辑框1.提示文本颜色 = 0XFFDFDFDF
		图片框1.图片资源 = 其他_资源.白色返回图标
		自定义列表框1.背景颜色 = 0XFF222222
		自定义列表框2.背景颜色 = 0XFF222222
	结束 方法

结束 类

类 其他_管理_标识管理器_方法 : 组件容器

	方法 生成identifier(ID : 文本)
		开始俘获异常()
		变量 A : 文本 = "data/identifier/identifier.json"
		变量 B : 文本 = 其他_变量.数据目录 + "identifier/" + ID + ".json"
		写出资源文件(取安卓环境(),A,B)
		俘获所有异常()
		结束俘获异常()
	结束 方法
	
	方法 批量生成identifier()
		开始俘获异常()
		变量 json : JSON对象 = 读入资源文件(取安卓环境(),"data/identifier.json")
		变量 list : JSON数组 = json["list"]
		循环(i, 0, list.长度)
			变量 ID : 文本 = list.取文本(i)
			变量 A : 文本 = "data/identifier/identifier.json"
			变量 B : 文本 = 其他_变量.数据目录 + "identifier/" + ID + ".json"
			如果 文件是否存在(B) 则
				否则
					写出资源文件(取安卓环境(),A,B)
			结束 如果
		结束 循环
		俘获所有异常()
		结束俘获异常()
	结束 方法

结束 类

类 其他_管理_标识管理器_列表 : 组件容器

	@布局配置([[根布局=真,宽度=-1,高度=-2]])
	变量 线性布局1 : 线性布局
	@布局配置([[父布局=线性布局1,宽度=-1,高度DP=50,圆角=25,卡片阴影=0,@上外边距DP=5]])
	变量 卡片布局1 : 卡片布局
	@布局配置([[父布局=卡片布局1,宽度=-1,高度=-1,背景颜色=-1,水波纹效果=真]])
	变量 线性布局2 : 线性布局
	@布局配置([[父布局=线性布局2,高度=-1,宽度DP=50,对齐方式=17]])
	变量 线性布局3 : 线性布局
	@布局配置([[父布局=线性布局3,宽度DP=35,高度DP=35]])
	变量 图片框1 : 图片框
	@布局配置([[父布局=线性布局2,宽度=-1,高度=-1,对齐方式=8388627,纵向布局=真]])
	变量 线性布局4 : 线性布局
	@布局配置([[父布局=线性布局4,@左外边距DP=5,字体大小=14,字体颜色=-16777216,内容="Name"]])
	变量 文本框1 : 文本框
	@布局配置([[父布局=线性布局4,@左外边距DP=5,字体大小=11,字体颜色=-16777216,内容="item_id"]])
	变量 文本框2 : 文本框
	@布局配置([[父布局=线性布局4,@左外边距DP=5,字体大小=10,字体颜色=-16777216,内容="data_0"]])
	变量 文本框3 : 文本框

	//声明
	变量 设置_设定 : 设置_设定

	事件 其他_管理_标识管理器_列表:创建完毕()
		//设置夜间模式
		如果 设置_设定.获取开关("NightMode") 则
			设置夜间模式()
		结束 如果
	结束 事件

	//设置功能按钮颜色
	方法 设置夜间模式()
		卡片布局1.卡片背景颜色 = 0XFF333333
		线性布局2.背景颜色 = 0XFF333333
		文本框1.字体颜色 = 0XFFFFFFFF
		文本框2.字体颜色 = 0XFFFFFFFF
		文本框3.字体颜色 = 0XFFFFFFFF
	结束 方法

结束 类


类 其他_管理_标识管理器_操作 : 组件容器

	@布局配置([[根布局=真,宽度=-1,高度=-1,纵向布局=真,对齐方式=17,左内边距DP=20,右内边距DP=20]])
	变量 线性布局1 : 线性布局
	@布局配置([[父布局=线性布局1,宽度=-1,高度DP=80,对齐方式=17,背景颜色=-1]])
	变量 线性布局2 : 线性布局
	@布局配置([[父布局=线性布局2,宽度DP=60,高度DP=60,圆角=30]])
	变量 卡片布局1 : 卡片布局
	@布局配置([[父布局=卡片布局1,宽度=-1,高度=-1,对齐方式=17,背景颜色=-1,水波纹效果=真]])
	变量 线性布局3 : 线性布局
	@布局配置([[父布局=线性布局3,宽度DP=45,高度DP=45,图片资源="../../../../资源库/图标/mores/identifier_manager/item_id.png"]])
	变量 图片框1 : 图片框
	@布局配置([[父布局=线性布局3,可视=假,内容="Xinyinglovesyou"]])
	变量 图标路径 : 文本框
	@布局配置([[父布局=线性布局1,宽度=-1,高度=-2,纵向布局=真,背景颜色=-1]])
	变量 线性布局4 : 线性布局
	@布局配置([[父布局=线性布局4,宽度=-1,高度DP=45]])
	变量 相对布局1 : 相对布局
	@布局配置([[父布局=相对布局1,宽度=-1,高度=-1,对齐方式=8388627]])
	变量 线性布局5 : 线性布局
	@布局配置([[父布局=线性布局5,内容="名称",字体颜色=-16777216,字体大小=15,@左外边距DP=10]])
	变量 文本框1 : 文本框
	@布局配置([[父布局=相对布局1,宽度=-1,高度=-1,对齐方式=8388629]])
	变量 线性布局6 : 线性布局
	@布局配置([[父布局=线性布局6,宽度DP=150,高度DP=35,@左外边距DP=0,@右外边距DP=10,背景颜色=-591879,对齐方式=17]])
	变量 线性布局7 : 线性布局
	@布局配置([[父布局=线性布局7,宽度=-1,字体大小=13,显示光标=真,单行输入=真,提示文本="请输入名称",提示文本颜色=-6842473,内容="",背景颜色=16777215,对齐方式=119,高度DP=35,右内边距DP=5,左内边距DP=5]])
	变量 编辑框1 : 编辑框
	@布局配置([[父布局=线性布局4,宽度=-1,高度DP=45]])
	变量 相对布局2 : 相对布局
	@布局配置([[父布局=相对布局2,宽度=-1,高度=-1,对齐方式=8388627]])
	变量 线性布局8 : 线性布局
	@布局配置([[父布局=线性布局8,内容="ID",字体颜色=-16777216,字体大小=15,@左外边距DP=10]])
	变量 文本框3 : 文本框
	@布局配置([[父布局=相对布局2,宽度=-1,高度=-1,对齐方式=8388629]])
	变量 线性布局11 : 线性布局
	@布局配置([[父布局=线性布局11,宽度DP=150,高度DP=35,@右外边距DP=10,背景颜色=-591879,对齐方式=17]])
	变量 线性布局13 : 线性布局
	@布局配置([[父布局=线性布局13,宽度=-1,字体大小=13,显示光标=真,单行输入=真,提示文本="请输入ID",提示文本颜色=-6842473,内容="",背景颜色=16777215,对齐方式=119,高度DP=35,右内边距DP=5,左内边距DP=5]])
	变量 编辑框2 : 编辑框
	@布局配置([[父布局=线性布局4,宽度=-1,高度DP=45]])
	变量 相对布局3 : 相对布局
	@布局配置([[父布局=相对布局3,宽度=-1,高度=-1,对齐方式=8388627]])
	变量 线性布局9 : 线性布局
	@布局配置([[父布局=线性布局9,内容="数据",字体颜色=-16777216,字体大小=15,@左外边距DP=10]])
	变量 文本框2 : 文本框
	@布局配置([[父布局=相对布局3,宽度=-1,高度=-1,对齐方式=8388629]])
	变量 线性布局10 : 线性布局
	@布局配置([[父布局=线性布局10,宽度DP=150,高度DP=35,@右外边距DP=10,背景颜色=-591879,对齐方式=17]])
	变量 线性布局12 : 线性布局
	@布局配置([[父布局=线性布局12,宽度=-1,字体大小=13,显示光标=真,单行输入=真,提示文本="请输入数据",提示文本颜色=-6842473,内容="",背景颜色=0,对齐方式=119,高度DP=35,右内边距DP=5,左内边距DP=5,输入方式=2]])
	变量 编辑框3 : 编辑框
	@布局配置([[父布局=线性布局1,宽度=-1,高度DP=40,对齐方式=17,背景颜色=-1,水波纹效果=真]])
	变量 线性布局14 : 线性布局
	@布局配置([[父布局=线性布局14,字体大小=15,字体颜色=-16777216,粗体=真,内容="创建"]])
	变量 文本框4 : 文本框
	@布局配置([[父布局=线性布局1,宽度=-1,高度DP=40,对齐方式=17,背景颜色=-1,水波纹效果=真]])
	变量 线性布局15 : 线性布局
	@布局配置([[父布局=线性布局15,字体大小=15,字体颜色=-16777216,粗体=真,内容="取消"]])
	变量 文本框5 : 文本框
	@布局配置([[父布局=线性布局1,内容="用于防止自动弹出输入法",可视=假]])
	变量 文本框防止自动弹出输入法 : 文本框

结束 类

类 其他_管理_标识管理器_资源

	@静态
	变量 列表图标 : 图片资源 = "../../../../资源库/图标/mores/identifier_manager/item_id.png"

结束 类