@安卓资源.图片("../../../../资源库/图标/mores/backup_manager/attribute.png")
@安卓资源.图片("../../../../资源库/图标/mores/backup_manager/delete.png")
@安卓资源.图片("../../../../资源库/图标/mores/backup_manager/export.png")

类 其他_管理_备份管理器 : 窗口

	@布局配置([[根布局=真,宽度=-1,高度=-1,纵向布局=真]])
	变量 线性布局1 : 线性布局
	@布局配置([[父布局=线性布局1,宽度=-1,高度DP=50,对齐方式=8388627,背景颜色=-1]])
	变量 线性布局2 : 线性布局
	@布局配置([[父布局=线性布局2,宽度DP=15,高度DP=15,图片资源="../../../../资源库/图标/back/Back_black.png",@左外边距DP=10]])
	变量 图片框1 : 图片框
	@布局配置([[父布局=线性布局2,字体大小=15,内容="项目",字体颜色=-16777216]])
	变量 文本框2 : 文本框
	@布局配置([[父布局=线性布局2,宽度DP=2,高度DP=12,@左外边距DP=10,卡片阴影=0]])
	变量 卡片布局1 : 卡片布局
	@布局配置([[父布局=卡片布局1,宽度=-1,高度=-1,背景颜色=-526087]])
	变量 线性布局3 : 线性布局
	@布局配置([[父布局=线性布局2,@左外边距DP=10,粗体=真,字体大小=16,内容="备份管理器",字体颜色=-16777216]])
	变量 文本框3 : 文本框
	@布局配置([[父布局=线性布局1,宽度=-1,高度=-1,纵向布局=真,内边距DP=5]])
	变量 线性布局4 : 线性布局
	@布局配置([[父布局=线性布局4,宽度=-1,高度=-1,分割线高度=0]])
	变量 自定义列表框1 : 自定义列表框

	//声明
	变量 设置_设定 : 设置_设定

	变量 列表集合 : 文本集合
	变量 文件路径SS : 文本

	事件 其他_管理_备份管理器:创建完毕()
		//设置主题
		本对象.显示标题栏 = 假
		本对象.状态栏颜色 = 0XFFFFFFFF
		本对象.状态栏字体黑色 = 真
		//窗口切换动画
		播放默认切换动画(2)
		//退出按钮
		事件 图片框1:被单击()
			关闭窗口()
		结束 事件
		事件 文本框2:被单击()
			关闭窗口()
		结束 事件
		//设置夜间模式
		如果 设置_设定.获取开关("NightMode") 则
			设置夜间模式()
		结束 如果
		//设置背景
		变量 设置相关路径 : 文本 = 设置_设定.获取文本("PersonalityBackground")
		如果 设置相关路径.替换(" ","") == "" 则
		否则
			如果 文件是否存在(设置相关路径) 则
				设置_设置.设置布局背景(本对象,线性布局4,设置相关路径)
			结束 如果
		结束 如果
		//加载列表
		加载列表(其他_变量.备份目录)
		//订阅事件
		订阅事件()
	结束 事件

	方法 加载列表(路径 : 文本,刷新 : 逻辑型 = 假)
		如果 刷新 则
			//刷新
			列表集合 = 文件操作.取子文件集合(路径)
			自定义列表框1.更新项目(列表集合.长度)
		否则
			//加载自定义列表
			事件 自定义列表框1:加载布局(索引 : 整数,项目布局 : 组件容器) : 组件容器
				变量 布局 : 其他_管理_备份管理器_列表?
				如果 项目布局 == 空 则
					布局 = 创建 其他_管理_备份管理器_列表
				否则
					布局 = 项目布局
				结束 如果
				//显示到列表
				变量 文件路径 : 文本 = 列表集合[索引]
				如果 是否为目录(文件路径) 则
					布局.线性布局1.可视 = 假
				否则
					布局.线性布局1.可视 = 真
					布局.文本框1.内容 = 取文件名(文件路径)
					如果 设置_设定.获取开关("BasicBackupManagerMD5") 则
						布局.文本框2.内容 = 其他_转换.转换文件大小(取文件大小(文件路径)) + " MD5: " + 取文件MD5(文件路径) + " 路径: " + 文件路径
					否则
						布局.文本框2.内容 = 其他_转换.转换文件大小(取文件大小(文件路径)) + " 路径: " + 文件路径
					结束 如果
				结束 如果
				//项目被单击
				事件 布局.线性布局2:被单击()
					变量 Array : 文本[] = {"文件属性","导入项目","删除备份"}
					变量 Icon : 整数[] = {R资源.取图片id("attribute"),R资源.取图片id("export"),R资源.取图片id("delete")}
					变量 菜单对话框 : XPopup列表弹窗
					菜单对话框.标题 = "菜单"
					菜单对话框.列表项目 = Array
					菜单对话框.列表图标 = Icon
					如果 设置_设定.获取开关("NightMode") 则
						菜单对话框.暗色主题(真)
					结束 如果
					菜单对话框.动画(XPopup动画.滑动_上)
					菜单对话框.显示()
					//列表被点击
					事件 菜单对话框:列表被单击(索引 : 整数,文本 : 文本)
						假如 索引
							是 0
								变量 文件属性 : 其他_工具_文件属性
								文件属性.打开(文件路径)
							是 1
								如果 ZIP解压(文件路径,其他_变量.项目目录 + 取文件前缀名(文件路径)) 则
									弹出成功提示("导入成功!")
								否则
									弹出错误提示("导入失败!")
								结束 如果
							是 2
								变量 删除对话框 : XPopup对话弹窗
								删除对话框.标题 = "提示"
								删除对话框.内容 = "是否删除备份：" + 取文件名(文件路径)
								如果 设置_设定.获取开关("NightMode") 则
									删除对话框.暗色主题(真)
								结束 如果
								删除对话框.显示()
								事件 删除对话框:确定()
									如果 删除文件(文件路径) 则
										弹出成功提示("删除备份成功！")
									否则
										弹出错误提示("删除备份失败！")
									结束 如果
									加载列表(其他_变量.备份目录,真)
								结束 事件
						结束 假如
					结束 事件
				结束 事件
				返回 布局
			结束 事件
			//转换为集合
			列表集合 = 文件操作.取子文件集合(路径)
			自定义列表框1.更新项目(列表集合.长度)
			//检测是否为空项目
			如果 列表集合.长度 == 0 则
				弹出通知("没有任何项目！")
			结束 如果
		结束 如果
	结束 方法

	//设置功能按钮颜色
	方法 设置夜间模式()
		本对象.状态栏颜色 = 0XFF333333
		本对象.状态栏字体黑色 = 假
		线性布局2.背景颜色 = 0XFF333333
		线性布局3.背景颜色 = 0XFF222222
		卡片布局1.卡片背景颜色 = 0XFF222222
		文本框2.字体颜色 = 0XFFFFFFFF
		文本框3.字体颜色 = 0XFFFFFFFF
		图片框1.图片资源 = 其他_资源.白色返回图标
		线性布局4.背景颜色 = 0XFF222222
	结束 方法

结束 类

类 其他_管理_备份管理器_列表 : 组件容器

	@布局配置([[根布局=真,宽度=-1,高度=-2]])
	变量 线性布局1 : 线性布局
	@布局配置([[父布局=线性布局1,宽度=-1,高度DP=45,圆角=15,卡片阴影=0]])
	变量 卡片布局1 : 卡片布局
	@布局配置([[父布局=卡片布局1,宽度=-1,高度DP=45,水波纹效果=真,背景颜色=-1]])
	变量 线性布局2 : 线性布局
	@布局配置([[父布局=线性布局2,宽度DP=45,高度DP=45,对齐方式=17]])
	变量 线性布局3 : 线性布局
	@布局配置([[父布局=线性布局3,宽度DP=30,高度DP=30,图片资源="../../../../资源库/图标/mores/backup_manager/list_icon.png"]])
	变量 图片框1 : 图片框
	@布局配置([[父布局=线性布局2,宽度=-1,高度=-1,纵向布局=真,对齐方式=8388627]])
	变量 线性布局4 : 线性布局
	@布局配置([[父布局=线性布局4,@左外边距DP=5,字体大小=15,字体颜色=-16777216,单行显示=真]])
	变量 文本框1 : 文本框
	@布局配置([[父布局=线性布局4,@左外边距DP=5,字体大小=12,跑马灯效果=真,单行显示=真]])
	变量 文本框2 : 文本框

	//声明
	变量 设置_设定 : 设置_设定

	事件 其他_管理_备份管理器_列表:创建完毕()
		//设置夜间模式
		如果 设置_设定.获取开关("NightMode") 则
			设置夜间模式()
		结束 如果
	结束 事件

	//设置功能按钮颜色
	方法 设置夜间模式()
		卡片布局1.卡片背景颜色 = 0XFF333333
		线性布局2.背景颜色 = 0XFF333333
		文本框1.字体颜色 = 0XFFFFFFFF
		文本框2.字体颜色 = 0XFFFFFFFF
	结束 方法

结束 类